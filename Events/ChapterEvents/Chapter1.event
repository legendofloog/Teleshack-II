EventPointerTable(Chapter1Events, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene


TurnBasedEvents:
TurnEventPlayer(0, TutorialZero, 1)
TurnEventPlayer(0, TutorialTime, 2)
TurnEventPlayer(0, AlenaSpawn, 2)
TurnEventPlayer(0, AlenaCharge, 3)
TurnEventPlayer(0,RaschwitzWarning,4)
TurnEventPlayer(0,LastBoroWarning,8)
TurnEventPlayer(0, BoroSpawn, 10)
END_MAIN

TutorialZero:
CenterTextbox(CheckTheGuide)
NoFade
ENDA

TutorialTime:
CameraCursor(Mira)
Text(Chapter1MiraCaptureText)
CenterTextbox(Chapter1CaptureStealTutorialBox)
NoFade
ENDA

AlenaSpawn:
CAMERA Alena
FlashCursor(Alena,30)
Text(AlenaSpawnText)
NoFade
ENDA

AlenaCharge:
CAMERA Alena
FlashCursor(Alena,30)
Text(AlenaChargeText)
SVAL s1 0x00040A06
CHAI Alena
SVAL s1 0x00000000
CHAI [1,13]
CHAI [1,15]
NoFade
ENDA

RaschwitzWarning:
CAMERA Raschwitz_Ch1
FlashCursor(Raschwitz_Ch1,60)
Text(RaschwitzWarningBoroText)
CenterTextbox(Chapter1EquipTutorialBox)
NoFade
ENDA

LastBoroWarning:
CAMERA Cleo
FlashCursor(Cleo,60)
Text(LastBoroWarningText)
NoFade
ENDA

BoroSpawn:
SpawnReinforcements(BoroUnitGroup)
CameraCursor(Boro_Ch1)
Text(BoroSpawnText)
ENUT 0x4
NoFade
ENDA

CharacterBasedEvents:
CharacterEventBothWays(7, LoeweIskraTalkCh1, Loewe, Iskra)
CharacterEventBothWays(14, SvetomirAlenaTalkCh1, Svetomir, Alena)
END_MAIN


LocationBasedEvents:
Door(3,10)
Door(10,13)
Chest(Mend,1,10)
Chest(SteelBow,10,11)
LOCA 9 LoeweSvetomirDoor [16,10] 0x12
LOCA 10 SennyDoor [16,3] 0x12
LOCA 11 HelmanDoor [10,5] 0x12
Escape(2,0)
Escape(3,0)
END_MAIN

MiscBasedEvents:
AREA 0 CivilianEscape [14, 0] [15, 0]
AFEV 12 LoeweSvetomirFreed 9
AFEV 13 SennyFreed 10
AFEV 15 HelmanFreed 11
AFEV 1 RaschwitzQuoteGone 18
AFEV 1 RaschwitzQuoteGone 19
AFEV 1 RaschwitzQuoteGone 20
AFEV 1 RaschwitzQuoteGone 21
AFEV 0x9F BoroQuoteGone 0x9C
AFEV 0x9F BoroQuoteGone 0x9D
AFEV 0x9F BoroQuoteGone 0x9E
CauseGameOverIfLordDies
END_MAIN

RaschwitzQuoteGone:
NoFade //doesn't actually need to be an event, just sets the battle quote
ENDA

BoroQuoteGone:
NoFade
ENDA

Dunno1:
END_MAIN


Dunno2:
END_MAIN

Dunno3:
END_MAIN


Tutorial:
END_MAIN

Units1:
UNIT Cleo Skyseeker_Dismounted   Cleo Level(3,Ally,False) [17,16] 0x0 0x0 0 0 [Argilabrys,ShortAxe,Vulnerary]     NoAI
UNIT

PlayerUnits1:
UNIT Cleo Skyseeker_Dismounted   Cleo Level(3,Ally,False) [19,16] 0x0 0x0 0x1 REDA17R16 [Argilabrys,IronAxe,Vulnerary]     NoAI
UNIT

Units2:
UNIT Mira ThiefFighter_F         Cleo Level(1,Ally,False) [19,16] 0x0 0x0 0x1 REDA18R16 [SilverSword,ShortSword,Vulnerary] NoAI
UNIT

Units3:
UNIT Iskra Mage_F                Cleo Level(1,Ally,False) [19,15] 0x0 0x0 0x1 REDA18R15 [Thunder,Vulnerary]                NoAI
UNIT Emil LanceKnight_Dismounted Cleo Level(1,Ally,False) [19,17] 0x0 0x0 0x1 REDA18R17 [ShortLance,Vulnerary,DoorKey]              NoAI
UNIT

GreenUnits:
UNIT Loewe Scaleshot_Dismounted Cleo Level(4,NPC,False) [18,10] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Svetomir Priest            Cleo Level(2,NPC,False) [18,12] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Senny Civilian_F_2         0x0 Level(1,NPC,False) [18,3] 0x0 0x0 0x0 0x0 [0x0] EscapeOnlyAI
UNIT Helman Civilian_2          0x0 Level(1,NPC,False) [10,7] 0x0 0x0 0x0 0x0 [0x0] EscapeOnlyAI
UNIT

Flashback1Units1:
UNIT Chapter1Generics Swordmaster_F 0x0 Level(1,NPC,False) [2,4] 0x0 0x0 0x0 0x0 [Kaiserschwert] NoAI
UNIT Mira Civilian_2 0x0 Level(1,NPC,False) [13,5] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Mira Civilian_F_2 0x0 Level(1,NPC,False) [1,6] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Mira Bard_F 0x0 Level(1,NPC,False) [5,7] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Mira LanceSoldier 0x0 Level(1,NPC,False) [10,8] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT

Flashback1Units2:
UNIT Helman Civilian Raschwitz_Ch1     Level(1,NPC,True) [14,2] 0x0 0x0 0x1 REDA9R1 [0x0] NoAI
UNIT Senny Civilian_F   0x0          Level(1,NPC,False) [14,1] 0x0 0x0 0x1 REDA8R2 [0x0] NoAI
UNIT Raschwitz_Ch1 Civilian 0x0    Level(1,NPC,False) [14,3] 0x0 0x0 0x1 REDA10R2 [0x0] NoAI
UNIT 

Flashback2Units1:
UNIT Helman Civilian Raschwitz_Ch1     Level(1,NPC,True) [14,2] 0x0 0x0 0x1 REDA8R2 [0x0] NoAI
UNIT 

Flashback2Units2:
UNIT Senny Sniper_F   0x0          Level(1,NPC,False) [14,2] 0x0 0x0 0x1 REDA9R2 [0x0] NoAI
UNIT Chapter1Generics AxeFighter 0x0    Level(1,NPC,False) [13,0] 0x0 0x0 0x1 REDA8R1 [0x0] NoAI
UNIT Svetomir Soldier 0x0 Level(1,NPC,False) [14,1] 0x0 0x0 0x1 REDA10R1 [0x0] NoAI
UNIT 

EnemyUnits:
UNIT Raschwitz_Ch1 BowArmor 0x0      Level(3,Enemy,False) [2,0] 0x0 0x0 0x0 0x0 [SteelBow, LeatherShield] AttackInRangeAI
UNIT Chapter1Generics AxeArmor 0x0      Level(1,Enemy,True) [3,0] 0x0 0x0 0x0 0x0 [IronAxe] NeverMoveAI
UNIT Chapter1Generics SwordFighter Raschwitz_Ch1 Level(1,Enemy,True) [13,16] 0x0 0x0 0x0 0x0 [ShortSword] PursueWithoutHeedAI
UNIT Chapter1Generics SwordArmor Raschwitz_Ch1 Level(1,Enemy,True) [4,8] 0x0 0x0 0x0 0x0 [IronSword] AttackInRangeAI
UNIT Chapter1Generics AxeSoldier Raschwitz_Ch1 Level(1,Enemy,True) [14,13] 0x0 0x0 0x0 0x0 [ShortAxe] PursueWithoutHeedAI
UNIT Chapter1Generics SwordFighter Raschwitz_Ch1 Level(1,Enemy,True) [14,7] 0x0 0x0 0x0 0x0 [ShortSword] AttackInRangeAI
UNIT Chapter1Generics LanceSoldier Raschwitz_Ch1 Level(1,Enemy,True) [10,14] 0x0 0x0 0x0 0x0 [ShortLance, DoorKey, ChestKey_5] NeverMoveAI
UNIT Chapter1Generics AxeSoldier Raschwitz_Ch1 Level(1,Enemy,True) [10,4] 0x0 0x0 0x0 0x0 [ShortAxe, DoorKey] NeverMoveAI
UNIT Chapter1Generics LanceSoldier Raschwitz_Ch1 Level(1,Enemy,True) [4,10] 0x0 0x0 0x0 0x0 [ShortLance, DoorKey] NeverMoveAI
UNIT Chapter1Generics LanceSoldier Raschwitz_Ch1 Level(1,Enemy,True) [15,3] 0x0 0x0 0x0 0x0 [ShortLance, DoorKey] NeverMoveAI
UNIT Chapter1Generics Archer Raschwitz_Ch1 Level(1,Enemy,True) [5,3] 0x0 0x0 0x0 0x0 [ShortBow] AttackInRangeAI
UNIT Chapter1Generics Archer Raschwitz_Ch1 Level(1,Enemy,True) [2,6] 0x0 0x0 0x0 0x0 [ShortBow] AttackInRangeAI
UNIT Chapter1Generics LanceArmor Raschwitz_Ch1 Level(1,Enemy,True) [15,1] 0x0 0x0 0x0 0x0 [IronLance] TwoActionAI
UNIT Chapter1Generics Freelancer Raschwitz_Ch1 Level(1,Enemy,True) [5,18] 0x0 0x0 0x0 0x0 [ShortLance] AttackInRangeAI
UNIT


Turn2Reinforcements:
UNIT Alena Myrmidon_F 0x0 Level(2,Enemy,False) [2,14] 0x0 0x0 0x0 0x0 [KillingEdge, IronSword, Vulnerary] DoNothingAI
UNIT Chapter1Generics Myrmidon    0x0 Level(1,Enemy,True)  [1,13] 0x0 0x0 0x0 0x0 [ShortSword] DoNothingAI
UNIT Chapter1Generics Myrmidon    0x0 Level(1,Enemy,True)  [1,15] 0x0 0x0 0x0 0x0 [ShortSword] DoNothingAI
UNIT


BoroUnitGroup:
UNIT Boro_Ch1 Halberdier 0x00 Level(3,Enemy,False) [19,16] 0x0 0x0 0x1 REDA19R16 [Impaler] PursueWithoutHeedAI
UNIT

BoroCutsceneGroup:
UNIT Boro_Ch1 Halberdier 0x00 Level(3,Enemy,False) [27,29] 0x0 0x0 0x0 0x0 [Impaler, SilverAxe] PursueWithoutHeedAI
UNIT

DraganaUnitGroup:
UNIT Senny Baron 0x0 Level(1,Enemy,False) [1,19] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Helman Sniper_F 0x0 Level(1,NPC,False) [16,16] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT

Traps1:
BlueSigil(2,0)
BlueSigil(3,0)
GreenSigil(14,0)
GreenSigil(15,0)
ENDTRAP
ALIGN 4


Traps2:
ENDTRAP
ALIGN 4


BeginningScene:
COUNTER_SET 0 0
ENUT GuideUnlockVigor
ENUT GuideUnlockLeadership
ENUT GuideUnlockRescue
ENUT GuideUnlockFreeActions
ENUT GuideUnlockCapture
ENUT GuideUnlockEquips
ENUT GuideUnlockSteal
ENUT GuideUnlockWeaponTriangle 
ENUT GuideUnlockPCC
ENUT GuideUnlockSupports
ENUT GuideUnlockCredits
ENUT GuideUnlockOrodreth
ENUT GuideUnlockByelen
ENUT GuideUnlockCythes
ENUT GuideUnlockDecia
ENUT GuideUnlockMahemen
ENUT GuideUnlockShove
ENUT GuideUnlockSkills
ENUT GuideUnlockFatigue
ENUT GuideUnlockDismount
ENUT GuideUnlockCanto
ENUT GuideUnlockEscape
ENUT GuideUnlockFormulaChanges
ENUT GuideUnlockDecianLiberators
ENUT GuideUnlockQoLChanges
LOMA Ch1LoeweVillage
LOAD1 1 Flashback1Units1
ENUN
FADU 15
MUSC WindyDayBirdsSoundEffect
BROWNBOXTEXT LinorasTopLeftText[8,8]
STAL 30
LOAD1 1 Flashback1Units2
ENUN
FlashCursor(Helman,60)
Text(Ch1KidLoeweTalksToFriendsText)
SingleUnitMove(Senny,15,3,8)
REMOVEUNIT Senny
SingleUnitMove(Raschwitz_Ch1,15,12,4)
REMOVEUNIT Raschwitz_Ch1
SingleUnitMove(Helman,15,3,3)
FlashCursor(Chapter1Generics,60)
SetBackground(Village1)
MUSC Explorer_RuinaForgottenRuinsID
Text(BeginningSceneLoeweFlashbackText)
SetBackground(OrodrethMap)
TEXTSTART
TEXTSHOW BeginningSceneLoeweHistoryLesson
TEXTEND
FlashCursor(10, 3, 60)
TEXTCONT
TEXTEND
FlashCursor(3, 5, 60)
TEXTCONT
TEXTEND
FlashCursor(6, 3, 60)
TEXTCONT
TEXTEND
FlashCursor(8, 9, 60)
TEXTCONT
TEXTEND
REMA
SetBackground(Village1)
Text(BeginningSceneLoeweHistoryLesson2)
FADI 15
TILECHANGE 0
REMOVEALL_GREEN
SetBackground(BlackBackground)
CGText(Ch1LoeweReminiscence)
WEATHER 5
EraseBackground
ENUN
FadeOutMusic
BROWNBOXTEXT Linoras1YearLaterTopLeftText[8,8]
LOAD1 1 Flashback2Units1
ENUN
STAL 20
FlashCursor(Helman,60)
MUSC FF5_Nostalgia
Text(BeginningSceneLoeweFlashback2Text1)
LOAD1 1 Flashback2Units2
ENUN
STAL 30
FlashCursor(Svetomir,30)
Text(Ch1BeginningSceneBoroFlashback2Text0)
FlashCursor(Chapter1Generics,30)
Text(Ch1BeginningSceneTrajanFlashback2Text0)
FlashCursor(Senny,60)
Text(Ch1BeginningSceneDraganaFlashback2Text0)
SingleUnitMove(Helman,15,10,2)
SingleUnitMove(Helman,15,10,5)
SingleUnitMove(Helman,15,12,4)
Text(BeginningSceneLoeweFlashback2Text2)
SingleUnitMove(Helman,15,11,6)
SingleUnitMove(Helman,15,11,9)
SingleUnitMove(Helman,15,13,8)
Text(BeginningSceneLoeweFlashback2Text3)
SingleUnitMove(Helman,15,13,9)
SingleUnitMove(Helman,15,7,6)
Text(BeginningSceneLoeweFlashback2Text4)
SingleUnitMove(Helman,15,5,9)
SingleUnitMove(Helman,15,3,8)
Text(BeginningSceneLoeweFlashback2Text4_5)
SingleUnitMove(Helman,10,3,9)
SingleUnitMove(Helman,10,1,9)
MOVE 10 Helman [2,4]
SingleUnitMove(Senny,15,3,4)
Text(BeginningSceneLoeweFlashback2Text5)
FlashCursor(2,2,60)
SingleUnitMove(Helman,15,2,2)
STAL 30
SingleUnitMove(Helman,15,2,4)
Text(BeginningSceneLoeweFlashback2Text6)
SingleUnitMove(Senny,15,6,0)
REMOVEUNIT Senny
SingleUnitMove(Chapter1Generics,15,6,0)
REMOVEUNIT Chapter1Generics
SingleUnitMove(Svetomir,15,7,0)
REMOVEUNIT Svetomir
SingleUnitMove(Helman,10,6,1)
STAL 20
Text(BeginningSceneLoeweFlashback2Text7)
SingleUnitMove(Helman,10,6,0)
REMOVEUNIT Helman
FADI 5
FadeOutMusic
WEATHER 0
REMOVEALL_GREEN
STAL 60
SetBackground(BlackBackground)		
CGText(Ch1SvetomirPesteringLoeweText1)
MUSC FE5_InTheBattle_Fate
CGText(Ch1SvetomirPesteringLoeweText2)
CGText(Ch1SvetomirPesteringLoeweText3)
CGText(Ch1SvetomirPesteringLoeweText4)
FADI 15
LOMA Chapter1
LOAD1 1 EnemyUnits
LOAD1 1 Turn2Reinforcements
LOAD1 1 GreenUnits
//LOAD1 1 UnitCapTestUnitGroup1
//LOAD1 1 UnitCapTestUnitGroup2
//LOAD1 1 UnitCapTestUnitGroup3
//LOAD1 1 UnitCapTestUnitGroup4
ENUN
//GotoPrepScreen
//NoFade
//ENDA
//OtherEvent:
CAMERA_CENTERED Loewe
FADU 15
BROWNBOXTEXT CastleSeleseTopLeftText[8,8]
FlashCursor(Loewe,60)
FlashCursor(Svetomir,60)
SetBackground(PrisonRoom)
Text(BeginningSceneLoeweGroupText)
EraseBackground
CAMERA_CENTERED [19,16]
FlashCursor(19,16,60)
FadeOutMusic
STAL 20
LOAD1 1 PlayerUnits1
ENUN
LOAD1 1 Units2
ENUN
LOAD1 1 Units3
ENUN 
FlashCursor(Cleo,60)
MUSC Sortie_Formation
SetBackground(CastleHallway)
Text(BeginningSceneCleoGroupText)
EraseBackground
NoFade
ENDA

LoeweIskraTalkCh1:
MUSS Rovers
Text(LoeweIskraTalkCh1Text)
GiveItem(Kaiserschwert_Fake,Loewe)
MURE 2
NoFade
ENDA

SvetomirAlenaTalkCh1:
MUSS WithUs_Song
Text(SvetomirAlenaTalkCh1Text)
NPCToAlly(Alena)
MURE 2
NoFade
ENDA

LoeweSvetomirDoor:
TILECHANGE 5
NoFade
ENDA


LoeweSvetomirFreed:
MUSS WithUs_Song
Text(LoeweSvetomirFreedText)
NPCToAlly(Loewe)
NPCToAlly(Svetomir)
GiveItem(IronBow,Loewe)
GiveItem(Heal,Svetomir)
SilentGiveItem(Vulnerary,Loewe)
SilentGiveItem(Vulnerary,Svetomir)
MURE 2
CenterTextbox(Chapter1EscapeTutorialBox)
NoFade
ENDA


SennyDoor:
TILECHANGE 0
NoFade
ENDA

SennyFreed:
Text(SennyFreedText)
NoFade
ENDA

HelmanDoor:
TILECHANGE 1
NoFade
ENDA

HelmanFreed:
Text(HelmanFreedText)
NoFade
ENDA

CivilianEscape:
CHECK_CLASS -1
SVAL s1 Civilian_F_2
BNE 4 s1 sC
	ENUT 0x78
	GOTO 5
LABEL 4
SVAL s1 Civilian_2
BNE 5 s1 sC
	ENUT 0x79
LABEL 5
NoFade
ENDA

EndingScene:
EndOfEscapeRoutineASMC
ENUT 0x84 //removes boss icons/talk icons etc
ENUN
MUSC 0x7FFF
FADI 10
STAL 30
RemoveAll
LOMA Ch1DraganaPrison
CAMERA [29,29]
SPAWN_ALLY Cleo [26,29]
SPAWN_ALLY Loewe [27,29]
FADU 15
STAL 10
MOVE 15 Cleo [20,26]
MOVE 15 Loewe [21,26]
ENUN
FlashCursor(Cleo,30)
FlashCursor(Loewe,30)
CHECK_EVENTID 7 //did Iskra not give loewe the sword
BNE 2 s0 sC
	CHECK_ALIVE Iskra
	BNE 420 s0 sC //branches if she is alive; if not, keep checking
		Text(EndingSceneTextLoeweAndCleoIskraDead)
		GiveItem(Kaiserschwert_Fake,Loewe)
		GOTO 907
	LABEL 420
	CheckUnitDeadOrJailed(Iskra)
	BEQ 57 s0 sC //if she's not removed, branch; if so, play captured dialogue
		Text(EndingSceneTextLoeweAndCleoIskraCaptured)
		GiveItem(Kaiserschwert_Fake,Loewe)
		GOTO 907
	LABEL 57 
	Text(EndingSceneTextLoeweAndCleoIskraAlive1)
	SPAWN_ALLY Iskra [20,16]
	CAMERA Iskra
	FlashCursor(Iskra,60)
	CamOn
	SingleUnitMove(Iskra, 15, 20, 25)
	CamOff
	CAMERA_CENTERED Iskra
	Text(EndingSceneTextLoeweAndCleoIskraAlive2)
	GiveItem(Kaiserschwert_Fake,Loewe)
	STAL 20
	SingleUnitMove(Iskra, 15, 20, 16)
	Text(EndingSceneTextLoeweAndCleoIskraAlive3)
	GOTO 907
LABEL 2
Text(EndingSceneTextLoeweAndCleo)
LABEL 907
MOVE 15 Cleo [20,15]
MOVE 15 Loewe [21,15]
ENUN
REMOVEALL_BLUE
STAL 60
MUSC FF5_Sealed_Away
LOAD1 1 BoroCutsceneGroup
ENUN
MOVE 15 Boro_Ch1 [20,26]
ENUN
FlashCursor(Boro_Ch1,60)
Text(EndingSceneTextBoro)
MOVE 15 Boro_Ch1 [20,15]
ENUN
FADI 15
LOMA Chapter2
CAMERA [1,19]
FADU 15
BROWNBOXTEXT CidorDungeonText[8,8]
LOAD1 1 DraganaUnitGroup
ENUN
CamOn
MOVE 15 Senny [16,19]
ENUN
CamOff
CAMERA_CENTERED Helman
SOUN 0x00B1
TILECHANGE 5
FlashCursor(Helman,60)
Text(EndingSceneTextDraganaAndCidor1)
SOUN 0x00B1
TILEREVERT 5
MOVE 15 Senny [0,11]
ENUN
FlashCursor(Helman,30)
Text(EndingSceneTextDraganaAndCidor2)
EVBIT_MODIFY 1
FADI 5
ENUF 0x84
ENUT 0x89
STAL 1
ENUN
MNC2 Chapter2
NoFade
ENDA