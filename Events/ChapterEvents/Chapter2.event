EventPointerTable(Chapter2Events, PointerList)

PointerList:
	POIN TurnBasedEvents
	POIN CharacterBasedEvents
	POIN LocationBasedEvents
	POIN MiscBasedEvents
	POIN Dunno1 Dunno2 Dunno3 Tutorial
	POIN Traps1 Traps2
	POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
	POIN $0 $0 $0
	POIN $0 $0 $0
	POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0, TutorialTime2, 1)
TurnEventPlayer(12, Turn3Spawn, 3)
TurnEventPlayer(13, BoroSpawn, 8)
TurnEventPlayer(14, BoroWarning, 7)
TurnEventPlayer(20, Turn5Spawn, 5)
END_MAIN

TutorialTime2:
CameraCursor(Cleo)
CenterTextbox(Chapter2Tutorial)
NoFade
ENDA

ALIGN 32
CharacterBasedEvents:
CharacterEventBothWays(8, EmilCleoTalkCh2, Emil, Cleo)
CharacterEventBothWays(0x11, DelilahSvetTalk, Delilah, Svetomir)
END_MAIN

EmilCleoTalkCh2:
Text(EmilCleoTalkCh2Text)
NoFade
ENDA


DelilahSvetTalk:
Text(DelilahFreedBySvetConvoText)
NoFade
ENDA

LocationBasedEvents:
Door(6,3)
Door(14,10)
Chest(LightRune,6,1)
Chest(SteelAxe,14,8)
LOCA 9 DelilahDoor [6,10] 0x12
LOCA 10 HernandezDoor [18,4] 0x12
LOCA 3 DraganaDoor [16,18] 0x12
END_MAIN

MiscBasedEvents:
AREA 0 CivilianEscape [19, 9] [19, 10]
AFEV 15 DelilahFreed 9
AFEV 16 HernandezFreed 10
CauseGameOverIfLordDies
AFEV 0 EndingScene 3
AFEV 1 TyrQuoteGone 0x15
AFEV 1 TyrQuoteGone 0x16
AFEV 1 TyrQuoteGone 0x17
AFEV 1 TyrQuoteGone 7
AFEV 0x9F BoroQuoteGone 0x9C
AFEV 0x9F BoroQuoteGone 0x9D
AFEV 0x9F BoroQuoteGone 0x9E
END_MAIN

TyrQuoteGone:
NoFade
ENDA

BoroQuoteGone:
NoFade
ENDA

Dunno1:

	END_MAIN

Dunno2:

	END_MAIN

Dunno3:

	END_MAIN

Tutorial:

	END_MAIN

Units1:
UNIT Svetomir Priest            Cleo Level(2,Ally,False) [0,2] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Loewe Priest            Cleo Level(2,Ally,False) [2,2] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra Mage_F                Cleo Level(1,Ally,False) [2,1] 0x0 0x0 0x0 0x1 [Thunder,Vulnerary]                NoAI
UNIT Emil LanceKnight_Dismounted Cleo Level(1,Ally,False) [0,1] 0x0 0x0 0x0 0x1 [IronLance,Vulnerary]              NoAI
UNIT Mira ThiefFighter_F           Cleo Level(1,Ally,False) [1,1] 0x0 0x0 0x0 0x1 [SilverSword,Shortsword,Vulnerary] NoAI
UNIT Alena Myrmidon_F 0x0 Level(3,Ally,False) [2,1] 0x0 0x0 0x0 0x0 [IronSword, Vulnerary] NoAI
UNIT Alena Myrmidon_F 0x0 Level(3,Ally,False) [0,0] 0x0 0x0 0x0 0x0 [IronSword, Vulnerary] NoAI
UNIT

MiraGroup:
UNIT Mira ThiefFighter_F            Cleo Level(2,Ally,False) [1,1] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT

IskraGroup:
UNIT Iskra Mage_F            Cleo Level(2,Ally,False) [0,1] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT

EmilGroup:
UNIT Emil LanceKnight_Dismounted            Cleo Level(2,Ally,False) [2,1] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT

SvetomirGroup:
UNIT Svetomir Priest            Cleo Level(2,Ally,False) [2,0] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT

AlenaGroup:
UNIT Alena Myrmidon            Cleo Level(2,Ally,False) [0,0] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT 

EnemyUnits:
UNIT Tyr_Ch2 AxeArmor 0x0      Level(4,Enemy,False) [11,18] 0x0 0x0 0x0 0x0 [SteelAxe, GoddessIcon] AttackInRangeAI
UNIT Chapter1Generics SwordFighter Tyr_Ch2 Level(2,Enemy,True) [1,12] 0x0 0x0 0x0 0x0 [Rapier] PursueWithoutHeedAI
UNIT Chapter1Generics AxeFighter Tyr_Ch2 Level(2,Enemy,True) [11,1] 0x0 0x0 0x0 0x0 [IronAxe] PursueWithoutHeedAI
UNIT Chapter1Generics LanceSoldier Tyr_Ch2 Level(2,Enemy,True) [14,11] 0x0 0x0 0x0 0x0 [ShortLance, DoorKey, ChestKey] NeverMoveAI
UNIT Chapter1Generics SwordArmor Tyr_Ch2 Level(2,Enemy,True) [6,4] 0x0 0x0 0x0 0x0 [SteelSword, DoorKey] NeverMoveAI
UNIT Chapter1Generics LanceSoldier Tyr_Ch2 Level(2,Enemy,True) [18,5] 0x0 0x0 0x0 0x0 [ShortLance, DoorKey] NeverMoveAI
UNIT Chapter1Generics SwordFighter Tyr_Ch2 Level(2,Enemy,True) [10,13] 0x0 0x0 0x0 0x0 [IronSword] AttackInRangeAI
UNIT Chapter1Generics AxeFighter Tyr_Ch2 Level(2,Enemy,True) [1,6] 0x0 0x0 0x0 0x0 [ShortAxe] AttackInRangeAI
UNIT Chapter1Generics Archer Tyr_Ch2 Level(2,Enemy,True) [6, 16] 0x0 0x0 0x0 0x0 [GaleBow] AttackInRangeAI
UNIT Chapter1Generics BowArmor Tyr_Ch2 Level(2,Enemy,True) [15, 19] 0x0 0x0 0x0 0x0 [Shortbow] TwoActionAI
UNIT Chapter1Generics LanceSoldier Tyr_Ch2 Level(2,Enemy,True) [3,4] 0x0 0x0 0x0 0x0 [IronLance] AttackInRangeAI
UNIT

GreenUnits:
UNIT Hernandez Brigand 0x0 Level(5,NPC,False) [18,2] 0x0 0x0 0x0 0x0 [0x0] EscapeOnlyAI
UNIT Dragana Sniper_F 0x0 Level(5,NPC,False) [16,16] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT Delilah Sister 0x0 Level(1,NPC,False) [6,8] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT

Turn3ReinforcementsBottom:
UNIT Chapter1Generics SwordFighter Tyr_Ch2 Level(2,Enemy,True) [2,19] 0x0 0x0 0x1 REDA2R18 [ShortSword] PursueWithoutHeedAI
UNIT Chapter1Generics AxeFighter Tyr_Ch2 Level(2,Enemy,True) [1,19] 0x0 0x0 0x1 REDA1R18 [ShortAxe] PursueWithoutHeedAI
UNIT

Turn3ReinforcementsRight:
UNIT Chapter1Generics LanceSoldier Tyr_Ch2 Level(2,Enemy,True) [19,9] 0x0 0x0 0x1 REDA18R9 [ShortLance] PursueWithoutHeedAI
UNIT

Turn5ReinforcementsBottom:
UNIT Chapter1Generics Myrmidon    0x0 Level(2,Enemy,True)  [1,19] 0x0 0x0 0x1 REDA1R19 [ShortSword] PursueWithoutHeedAI
UNIT

Turn5ReinforcementsRight:
UNIT Chapter1Generics AxeSoldier Tyr_Ch2 Level(2,Enemy,True) [19,9] 0x0 0x0 0x1 REDA18R9 [ShortAxe] PursueWithoutHeedAI
UNIT

BoroUnitGroup2:
UNIT Boro_Ch1 Halberdier 0x00 Level(1,Enemy,False) [1,0] 0x0 0x0 0x1 REDA1R0 [Javelin, SilverAxe, SilverLance] PursueWithoutHeedAI
UNIT

OpeningCutsceneUnits:
UNIT Varga SwordArmor           Cleo Level(5,Enemy,False) [2,3] 0x0 0x0 0x0 0x1 [LongSword,ShortSword, Vulnerary, DoorKey] NoAI // moves to 1,2
UNIT Ch2_Selese_Guy LanceSoldier Tyr_Ch2 Level(1,Enemy,True) [1,3] 0x0 0x0 0x0 0x0 [IronLance, DoorKey, ChestKey] NeverMoveAI
UNIT

OpeningCutsceneAllyUnits:
UNIT Cleo Skyseeker_Dismounted Cleo Level(2,Ally,False) [0,0] 0x0 0x0 0x0 0x1 [Argilabrys,IronAxe,Vulnerary]     NoAI //moves to 0,2
UNIT Loewe Scaleshot_Dismounted Cleo Level(5,Ally,False) [1,0] 0x0 0x0 0x0 0x1 [0x0] NoAI //moves to 2,2
UNIT

EndingCutsceneUnits:
UNIT Cleo Skyseeker_Dismounted Cleo Level(2,Ally,False) [14,19] 0x0 0x0 0x0 0x1 [Argilabrys,IronAxe,Vulnerary]     NoAI
UNIT Loewe Scaleshot_Dismounted Cleo Level(5,Ally,False) [15,19] 0x0 0x0 0x0 0x1 [0x0] NoAI 
UNIT Dragana Sniper_F 0x0 Level(5,NPC,False) [16,16] 0x0 0x0 0x0 0x0 [0x0] NoAI
UNIT

EndingCutsceneEnemies1:
UNIT Hernandez LanceSoldier Tyr_Ch2 Level(1,Enemy,True) [1,19] 0x0 0x0 0x1 REDA8R11 [IronLance, DoorKey, ChestKey] NeverMoveAI
UNIT Helman LanceSoldier Tyr_Ch2 Level(1,Enemy,True) [2,19] 0x0 0x0 0x1 REDA8R12 [IronLance, DoorKey, ChestKey] NeverMoveAI
UNIT Senny LanceSoldier Tyr_Ch2 Level(1,Enemy,True) [3,19] 0x0 0x0 0x1 REDA8R13 [IronLance, DoorKey, ChestKey] NeverMoveAI
UNIT Ch2_Selese_Guy LanceSoldier Tyr_Ch2 Level(1,Enemy,True) [0,19] 0x0 0x0 0x1 REDA10R12 [IronLance, DoorKey, ChestKey] NeverMoveAI
UNIT


EndingCutsceneUnits3:
UNIT Tyr_Ch2 LanceSoldier Tyr_Ch2 Level(1,Enemy,True) [17,6] 0x0 0x0 0x1 REDA12R11 [IronLance, DoorKey, ChestKey] NeverMoveAI
UNIT Chapter1Generics LanceSoldier Tyr_Ch2 Level(1,Enemy,True) [18,6] 0x0 0x0 0x1 REDA13R13 [IronLance, DoorKey, ChestKey] NeverMoveAI
UNIT Raschwitz_Ch1 LanceSoldier Tyr_Ch2 Level(1,Enemy,True) [10,6] 0x0 0x0 0x1 REDA10R10 [IronLance, DoorKey, ChestKey] NeverMoveAI
UNIT 0x4B LanceSoldier Tyr_Ch2 Level(1,Enemy,True) [11,6] 0x0 0x0 0x1 REDA11R10 [IronLance, DoorKey, ChestKey] NeverMoveAI
UNIT Boro_Ch1 Halberdier 0x00 Level(5,NPC,False) [18,5] 0x0 0x0 0x0 0x0 [Javelin] PursueWithoutHeedAI
UNIT

EndingCutsceneUnits2:
UNIT Hernandez LanceSoldier Tyr_Ch2 Level(1,Enemy,True) [1,19] 0x0 0x0 0x0 REDA7R11 [IronLance, DoorKey, ChestKey] NeverMoveAI
UNIT Helman LanceSoldier Tyr_Ch2 Level(1,Enemy,True) [2,19] 0x0 0x0 0x0 REDA7R12 [IronLance, DoorKey, ChestKey] NeverMoveAI
UNIT Senny LanceSoldier Tyr_Ch2 Level(1,Enemy,True) [3,19] 0x0 0x0 0x0 REDA7R13 [IronLance, DoorKey, ChestKey] NeverMoveAI
UNIT

Traps1:
GreenSigil(19,9)
GreenSigil(19,10)
ENDTRAP
ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4

BeginningScene:
	LOAD1 1 EnemyUnits
	LOAD1 1 OpeningCutsceneUnits
	LOAD1 1 GreenUnits
	ENUN
	SetBackground(CastleHallway)
	MUSC FFTAnxiety
	Text(Chapter2OpeningScene1)
	CALL $9EE2C4
	FlashCursor(Varga,60)
	Text(Chapter2OpeningScene2_1)
	LOAD1 1 OpeningCutsceneAllyUnits
	ENUN
	FlashCursor(Cleo,30)
	Text(Chapter2OpeningScene2_2)
	FadeOutMusic
	Text(Chapter2OpeningScene3)
	CUSN Varga
	MUSC Sortie_Formation
	StartBattle
	NormalDamage(0,14)
	NormalDamage(1,1)
	EndAttack
	SetScriptedBattle(80,15,10,90,1,0)
	FIGHT_MAP Varga Ch2_Selese_Guy LongSword False
	ENUN
	MOVE 15 Cleo [0,3]
	ENUN
	StartBattle
	CriticalHit(0,20)
	EndAttack
	SetScriptedBattle(100,10,5,70,9,0)
	FIGHT_MAP Cleo Ch2_Selese_Guy Argilabrys False
	ENUN
	AWAIT_KILL Ch2_Selese_Guy
	MOVE 15 Loewe [2,2]
	ENUN
	Text(Chapter2OpeningScene4)
	MOVE 15 Varga [2,2]
	MOVE 15 Cleo [1,2]
	MOVE 15 Loewe [0,2]
	ENUN
	NPCToAlly(Varga)
	STAL 30
	CAMERA Dragana
	FlashCursor(Tyr_Ch2,60)
	FlashCursor(Dragana,60)
	CheckIfExistentAllyThenLoadGroup(Mira, 6, MiraGroup)
	CheckIfExistentAllyThenLoadGroup(Iskra, 7, IskraGroup)
	CheckIfExistentAllyThenLoadGroup(Emil, 8, EmilGroup)
	CheckIfExistentAllyThenLoadGroup(Svetomir, 9, SvetomirGroup)
	CheckIfExistentAllyThenLoadGroup(Alena, 10, AlenaGroup)
	SVAL s1 0x1 //unset variety of things that might cause varga/cleo support to not be active, since no preps screen means it's not being handled? idk it's inconsistent
	UNSET_CONDITION Cleo
	SVAL s1 0x8
	UNSET_CONDITION Cleo
	Text(Chapter2OpeningScene5)
	NoFade
	ENDA

Turn3Spawn:
SpawnReinforcements(Turn3ReinforcementsBottom)
SpawnReinforcements(Turn3ReinforcementsRight)
NoFade
ENDA

Turn5Spawn:
SpawnReinforcements(Turn5ReinforcementsBottom)
SpawnReinforcements(Turn5ReinforcementsRight)
NoFade
ENDA

BoroSpawn:
CHECK_EVENTID 0xA7 //was boro's ass kicked
BNE 776 s0 sC //if sC = 1, it was, so we skip the spawn
SpawnReinforcements(BoroUnitGroup2)
CameraCursor(Boro_Ch1)
Text(Chapter2BoroSpawnText)
ENUT 0x4
LABEL 776
NoFade
ENDA

BoroWarning:
CHECK_EVENTID 0xA7 //was boro's ass kicked
BNE 776 s0 sC //if sC = 1, it was, so we skip the warning
CAMERA Loewe
FlashCursor(Loewe,60)
Text(Chapter2BoroWarning)
LABEL 776
NoFade
ENDA

DelilahDoor:
TILECHANGE 3
NoFade
ENDA

DelilahFreed:
CHECK_ACTIVE
SVAL s1 Svetomir
MUSC WithUs_Song
BNE 1 s1 sC
	Text(DelilahFreedBySvetText)
	ENUT 0x11
	GOTO 2
LABEL 1
Text(DelilahFreedText)
LABEL 2
NPCToAlly(Delilah)
GiveItem(Heal,Delilah)
SilentGiveItem(Vulnerary,Delilah)
NoFade
ENDA

HernandezDoor:
TILECHANGE 1
NoFade
ENDA

HernandezFreed:
Text(HernandezFreedText)
NoFade
ENDA

CivilianEscape:
CHECK_CLASS -1
SVAL s1 Brigand
BNE 4 s1 sC
	ENUT 0x7A
LABEL 4
NoFade
ENDA

DraganaDoor:
TILECHANGE 5
NoFade
ENDA

EndingScene:
CameraCursor(Dragana)
Text(Ch2DraganaWinResponseText)
CALL GiveBackKaiserschwertFake
CHECK_EXISTS Boro_Ch1
BEQ 776 s0 sC // if boro doesn't exist, then we skip him saying something at the end of the map
	STAL 30
	CameraCursor(Boro_Ch1)
	Text(Ch2BoroWinResponseText)
LABEL 776
STAL 30
MUSC 0x7FFF
FADI 15
CheckMaxHP(Cleo)
SADD s1 sC s0
SET_HP Cleo
CheckMaxHP(Loewe)
SADD s1 sC s0
SET_HP Loewe
RemoveAllDouble
LOAD1 1 EndingCutsceneUnits
ENUN
CAMERA_CENTERED Dragana
STAL 5
FADU 15
MOVE 15 Dragana [16,19]
ENUN
STAL 20
MOVE 15 Dragana [10,16]
MOVE 15 Cleo [12,16]
MOVE 15 Loewe [11,17]
ENUN
EVBIT_T 5
MUSC Rovers
TEXTSTART
TEXTSHOW Chapter2EndingScene1
TEXTEND
MOVE 15 Loewe [10,9]
ENUN
TEXTCONT
TEXTEND
REMA
ENUN
MUSC FE5_InTheBattle_Fate
MOVE 15 Cleo [11,14]
MOVE 15 Loewe [10,14]
MOVE 15 Dragana [11,15]
ENUN
LOAD1 1 EndingCutsceneEnemies1
ENUN
LOAD1 1 EndingCutsceneUnits3
ENUN
TEXTSTART
TEXTSHOW Chapter2EndingScene2
TEXTEND
MOVE 15 Boro_Ch1 [13,11]
ENUN
KILL Tyr_Ch2
AWAIT_KILL Tyr_Ch2
MOVE 15 Boro_Ch1 [13,12]
ENUN
KILL Chapter1Generics
AWAIT_KILL Chapter1Generics
TEXTCONT
TEXTEND
REMA
MOVE 15 Boro_Ch1 [10,13]
ENUN
StartBattle
CriticalHit(0,25)
EndAttack
SetScriptedBattle(100,25,10,80,8,0)
FIGHT_MAP Boro_Ch1 Ch2_Selese_Guy SilverLance False
ENUN
Text(Chapter2EndingScene3)
LOAD1 1 EndingCutsceneUnits2
ENUN
MOVE 15 Loewe [19,10]
MOVE 15 Cleo [19,9]
MOVE 15 Dragana [14,12]
ENUN
HIDEUNIT Loewe
HIDEUNIT Cleo 
TEXTSTART
TEXTSHOW Chapter2EndingScene4
TEXTEND
MOVE 15 Dragana [19,10]
ENUN
REMOVEUNIT Dragana
TEXTCONT
TEXTEND
REMA
EVBIT_F 5
FADI 15
SHOWUNIT Loewe
SHOWUNIT Cleo
MNC2 Chapter3
NoFade
ENDA
