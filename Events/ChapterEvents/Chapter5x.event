EventPointerTable(Ch5xInterludeEvents, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1
POIN $0 $0 $0 
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0, InterludeExplanation, 1)
END_MAIN

InterludeExplanation:
CenterTextbox(InterludeExplanationText)
NoFade
ENDA

CharacterBasedEvents:
CharacterEventBothWays(13, MishRecruit, Cleo, Mish)
CharacterEventBothWays(14, TobieRecruit, Cleo, Tobie)
CharacterEventBothWays(15, AwaRecruit, Cleo, Awa)
CharacterEventBothWays(16, CadenzaRecruit, Cleo, Cadenza)
CharacterEventBothWays(17, FaolanRecruit, Cleo, Faolan)
CharacterEventBothWays(18, MatthiasRecruit, Cleo, Matthias)
CharacterEventBothWays(10, IskraMiraTalk, Cleo, IskraCopy)
CharacterEventBothWays(10, IskraMiraTalk, Cleo, MiraCopy)
CharacterEventBothWays(11, VargaTalk, Cleo, VargaCopy)
CharacterEventBothWays(12, KwameTalk, Cleo, Kwame)
CharacterEventBothWays(0, TrajanTalk, Cleo, Trajan)
END_MAIN

MatthiasRecruit:
CHECK_EVENTID 21
BNE 98 s0 sC
	ENUT 21
	Text(MatthiasFirstTalkDialogue)
LABEL 98
PreventSkipping
Text(MatthiasPickDialogue)
AllowSkipping
SVAL s1 0x1
BEQ 99 s1 sC
	Text(MatthiasRejectedDialogue)
	ENUF 18
	NoFade
	ENDB
LABEL 99
Text(MatthiasAcceptedDialogue)
CUSA Matthias
SetFatigue(Matthias,0)
MOVE 15 Matthias [14,6]
ENUN
SVAL s1 0x1
SET_CONDITION Matthias
CALL CheckIfDoneSelecting
NoFade
ENDA

MishRecruit:
CHECK_EVENTID 7
BNE 4 s0 sC
	ENUT 7
	CHECK_ALIVE Mira
	BEQ 5 s0 sC
		Text(MishFirstTalkDialogueMiraAlive)
		GOTO 4
	LABEL 5
		Text(MishFirstTalkDialogueMiraDead)
LABEL 4
PreventSkipping
Text(MishPickDialogue)
AllowSkipping
SVAL s1 0x1
BEQ 6 s1 sC
	Text(MishRejectedDialogue)
	ENUF 13
	NoFade
	ENDB
LABEL 6
Text(MishAcceptedDialogue)
CHECK_EVENTID 14
BEQ 14 s0 sC  
	CUSA Mish
	SetFatigue(Mish,0)
	MOVE 15 Mish [14,7]
	ENUN
	SVAL s1 0x1
	SET_CONDITION Mish
	ENUN
LABEL 14
CALL CheckIfDoneSelecting
NoFade
ENDA

TobieRecruit:
SetBackground(0x33)
CHECK_EVENTID 8
BNE 8 s0 sC
	ENUT 8
	Text(TobieFirstTalkDialogue)
LABEL 8
PreventSkipping
Text(TobiePickDialogue)
AllowSkipping
SVAL s1 0x1
BEQ 9 s1 sC
	Text(TobieRejectedDialogue)
	CALL $9EE2C4
	ENUF 14
	NoFade
	ENDB
LABEL 9
Text(TobieAcceptedDialogue)
CALL $9EE2C4
CUSA Tobie
SetFatigue(Tobie,0)
MOVE 15 Tobie [14,6]
ENUN
SVAL s1 0x1
SET_CONDITION Tobie
CALL CheckIfDoneSelecting
NoFade
ENDA

AwaRecruit:
CHECK_EVENTID 9
BNE 10 s0 sC
	ENUT 9
	Text(AwaFirstTalkDialogue)
LABEL 10
PreventSkipping
Text(AwaPickDialogue)
AllowSkipping
SVAL s1 0x1
BEQ 11 s1 sC
	Text(AwaRejectedDialogue)
	ENUF 15
	NoFade
	ENDB
LABEL 11
Text(AwaAcceptedDialogue)
CUSA Awa
SetFatigue(Awa,0)
MOVE 15 Awa [14,6]
ENUN
SVAL s1 0x1
SET_CONDITION Awa
CALL CheckIfDoneSelecting
NoFade
ENDA

CadenzaRecruit:
CHECK_EVENTID 19
BNE 18 s0 sC
	ENUT 19
	Text(CadenzaFirstTalkDialogue)
LABEL 18
PreventSkipping
Text(CadenzaPickDialogue)
AllowSkipping
SVAL s1 0x1
BEQ 19 s1 sC
	Text(CadenzaRejectedDialogue)
	ENUF 16
	NoFade
	ENDB
LABEL 19
Text(CadenzaAcceptedDialogue)
CUSA Cadenza
SetFatigue(Cadenza,0)
MOVE 15 Cadenza [14,7]
ENUN
SVAL s1 0x1
SET_CONDITION Cadenza
CALL CheckIfDoneSelecting
NoFade
ENDA

FaolanRecruit:
CHECK_EVENTID 20
BNE 25 s0 sC
	Text(FaolanFirstTalkDialogue)
	ENUT 20
LABEL 25
PreventSkipping
Text(FaolanPickDialogue)
AllowSkipping
SVAL s1 0x1
BEQ 26 s1 sC
	Text(FaolanRejectedDialogue)
	ENUF 17
	NoFade
	ENDB
LABEL 26
Text(FaolanAcceptedDialogue)
CUSA Faolan
SetFatigue(Faolan,0)
MOVE 15 Faolan [14,7]
ENUN
SVAL s1 0x1
SET_CONDITION Faolan
CALL CheckIfDoneSelecting
NoFade
ENDA


IskraMiraTalk:
CheckUnitDeadOrJailed(Iskra)
BNE 16 s0 sC //branches if iskra is dead or jailed
	CheckUnitDeadOrJailed(Mira)
	BNE 17 s0 sC // branches if mira is dead or jailed
		SetBackground(0x33)
		Text(Ch5xMiraIskraTalk)
		CALL $9EE2C4
		NoFade
		ENDB
	LABEL 17
		CHECK_ALIVE Mira
		BNE 567 s0 sC // branches if she is captured
			Text(Ch5xIskraTalkMiraDead)
			NoFade
			ENDB
		LABEL 567
		Text(Ch5xIskraTalkMiraCaptured)
		NoFade
		ENDB
LABEL 16
CHECK_ALIVE Iskra
BNE 789 s0 sC //branches if she is captured
	Text(Ch5xMiraTalkIskraDead)
	NoFade
	ENDB
LABEL 789
Text(Ch5xMiraTalkIskraCaptured)
NoFade
ENDA

CheckIfDoneSelecting:
COUNTER_INC 0
COUNTER_CHECK 0
SVAL s1 2
BNE 13 s1 sC
	CHECK_EVENTID 13
	BEQ 15 s0 sC 
		CHECK_AT [2, 8]
		SVAL s1 Mish
		BNE 15 s1 sC
			CUSA Mish
			SetFatigue(Mish,0)
			MOVE 15 Mish [14,7]
			ENUN
			SVAL s1 0x1
			SET_CONDITION Mish
			ENUN
	LABEL 15
	Text(Ch5xRecruitedTwoUnits)
	ENUT 13
	ENUT 14
	ENUT 15
	ENUT 16
	ENUT 17
	ENUT 18
LABEL 13
NoFade
ENDA

VargaTalk:
Text(Ch5xCleoVargaTalk)
NoFade
ENDA

KwameTalk:
Text(Ch5xKwameLoeweTalk)
NoFade
ENDA

TrajanTalk:
COUNTER_CHECK 0
SVAL s1 2
BEQ 7 s1 sC
	Text(Ch5xTrajanStopsYou)
	NoFade
	ENDB
LABEL 7
Text(Ch5xTrajanConfirmation1)
PreventSkipping
Text(Ch5xTrajanConfirmation2)
AllowSkipping
SVAL s1 0x1
BEQ 12 s1 sC
	Text(Ch5xTrajanRejection)
	NoFade
	ENDB
	GOTO 30
LABEL 12
CALL EndingScene
LABEL 30
NoFade
ENDA

LocationBasedEvents:
END_MAIN

MiscBasedEvents:
AFEV 0 RefreshCleo 0
END_MAIN

RefreshCleo:
SVAL s1 0x42
UNSET_CONDITION Cleo
NoFade
ENDA

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

Traps1:
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

Units1:
UNIT Cleo Skyseeker_Dismounted Cleo Level(2,Ally,False) [8,3] 0x0 0x0 0x0 0x1 [Argilabrys,IronAxe,Vulnerary]     NoAI
UNIT

CleoSelectionUnitGroup:
UNIT Cleo Skyseeker_Dismounted Cleo Level(2,Ally,False) [8,3] 0x0 0x0 0x0 0x1 [Argilabrys,IronAxe,Vulnerary]     NoAI
UNIT

GreenUnits:
UNIT Gimm Thief Cleo Level(4,NPC,False) [2,8] 0x0 0x0 0x0 0x1 [DevilDagger,Vulnerary]     NoAI
UNIT Tobie PegasusRider_Dismounted Cleo Level(5,NPC,False) [2,9] 0x0 0x0 0x0 0x1 [SlimLance,LongLance,Vulnerary]     NoAI
UNIT Awa AxeKnight_Dismounted Cleo Level(5,NPC,False) [9,8] 0x0 0x0 0x0 0x1 [IronAxe,Poleaxe,Vulnerary] NoAI
UNIT Trajan Hero Cleo Level(5,NPC,False) [8,2] 0x0 0x0 0x0 0x1 [MasterAxe,IronBlade]     NoAI
UNIT Kwame Civilian_2 Cleo Level(1,NPC,False) [11,1] 0x0 0x0 0x0 0x1 [0x0]     NoAI
UNIT Cadenza Bard Cleo Level(5,NPC,False) [13,6] 0x0 0x0 0x0 0x1 [Wind, BalladOfWar]     NoAI
UNIT Faolan RookKnight_Dismounted Cleo Level(5,NPC,False) [6,4] 0x0 0x0 0x0 0x1 [Rapier, Javelin, Vulnerary, LightRune]     NoAI
UNIT Matthias Devotee_M Cleo Level(5, NPC, False) [0,3] 0x0 0x0 0x0 0x0 [Lumen,Heal,Vulnerary] NoAI
UNIT

IskraCopyGroup:
UNIT IskraCopy Mage_F   Cleo Level(2,NPC,False) [5,8] 0x0 0x0 0x0 0x1 [0x0]     NoAI
UNIT

MiraCopyGroup:
UNIT MiraCopy ThiefFighter_F   Cleo Level(2,NPC,False) [5,9] 0x0 0x0 0x0 0x1 [0x0]      NoAI
UNIT

VargaCopyGroup:
UNIT VargaCopy SwordArmor   Cleo Level(2,NPC,False) [4,2] 0x0 0x0 0x0 0x1 [0x0]      NoAI
UNIT

HeadquartersBeginningCutsceneUnits:
UNIT Cleo Skyseeker_Dismounted   Cleo Level(2,Ally,False) [0,3] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT Loewe Skyseeker_Dismounted   Cleo Level(2,Ally,False) [1,3] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]     NoAI
UNIT Trajan Hero Cleo Level(5,NPC,False) [1,4] 0x0 0x0 0x0 0x0 [MasterAxe,IronBlade]     NoAI
UNIT Kwame Civilian_2 Cleo Level(1,NPC,False) [0,4] 0x0 0x0 0x0 0x0 [0x0]     NoAI
UNIT

WarRoomUnits:
UNIT Cleo Skyseeker_Dismounted   Cleo Level(2,Ally,False) [9,9] 0x0 0x0 0x1 REDA10R5 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT Loewe Skyseeker_Dismounted   Cleo Level(2,Ally,False) [5,9] 0x0 0x0 0x1 REDA5R5 [Argilabrys,IronAxe,Vulnerary]     NoAI
UNIT Trajan Hero Cleo Level(5,NPC,False) [6,9] 0x0 0x0 0x1 REDA5R6 [MasterAxe,IronBlade]     NoAI
UNIT Kwame Civilian_2 Cleo Level(1,NPC,False) [8,9] 0x0 0x0 0x1 REDA10R6 [0x0]     NoAI
UNIT

LoeweBeginningCutsceneGroup:
UNIT Loewe Skyseeker_Dismounted   Cleo Level(2,Ally,False) [6,5] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]     NoAI
UNIT Kwame Civilian_2 Cleo Level(1,NPC,False) [4,8] 0x0 0x0 0x0 0x0 [0x0]     NoAI
UNIT

CleoBeginningCutsceneGroup:
UNIT Cleo Skyseeker_Dismounted Cleo Level(2,Ally,False) [9,8] 0x0 0x0 0x0 0x1 [Argilabrys,IronAxe,Vulnerary]     NoAI
UNIT

MiraBeginningCutsceneGroup:
UNIT MiraCopy ThiefFighter_F   Cleo Level(2,Ally,False) [9,8] 0x0 0x0 0x0 0x1 [Argilabrys,IronAxe,Vulnerary]     NoAI
UNIT


TrajanBeginningGroup:
UNIT Trajan Hero Cleo Level(5,NPC,False) [19,10] 0x0 0x0 0x1 REDA17R10 [MasterAxe,IronBlade]     NoAI
UNIT Senny AxeSoldier   Cleo Level(2,NPC,False) [19,9] 0x0 0x0 0x1 REDA18R9 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT Helman LanceSoldier   Cleo Level(2,NPC,False) [19,11] 0x0 0x0 0x1 REDA18R11 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT

LoeweEndingCutsceneGroup:
UNIT Cleo Skyseeker_Dismounted   Cleo Level(2,Ally,False) [7,4] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT Loewe Skyseeker_Dismounted   Cleo Level(2,Ally,False) [9,4] 0x0 0x0 0x0 REDA5R5 [Argilabrys,IronAxe,Vulnerary]     NoAI
UNIT Trajan Hero Cleo Level(5,NPC,False) [8,2] 0x0 0x0 0x0 0x0 [MasterAxe,IronBlade]     NoAI
UNIT

GechrysosCutsceneGroup:
UNIT Chapter1Generics SwordArmor Cleo Level(2,Enemy,False) [2,11] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT Chapter1Generics AxeArmor Cleo Level(2,Enemy,False) [2,12] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT Chapter1Generics SwordArmor Cleo Level(2,Enemy,False) [13,11] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT Chapter1Generics LanceArmor Cleo Level(2,Enemy,False) [13,12] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT Chapter1Generics AxeArmor Cleo Level(2,Enemy,False) [9,3] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT Chapter1Generics LanceArmor Cleo Level(2,Enemy,False) [10,3] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT Chapter1Generics General Cleo Level(2,Enemy,False) [3,3] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT Chapter1Generics Halberdier Cleo Level(2,Enemy,False) [5,4] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT Chapter1Generics Hero Cleo Level(2,Enemy,False) [12,6] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT 

EliasUnitGroup:
UNIT Helman Dracoknight Cleo Level(2,Enemy,False) [0,12] 0x0 0x0 0x0 0x0 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT

LeonidasUnitGroup:
UNIT Senny GoldKnight Cleo Level(2,Enemy,False) [14,11] 0x0 0x0 0x1 REDA9R12 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT

OrfeasUnitGroup:
UNIT Orfeas Emperor Cleo Level(2,Enemy,False) [8,10] 0x0 0x0 0x1 REDA7R10 [Argilabrys,IronAxe,Vulnerary]	NoAI
UNIT Boro_Ch1 Noble_F Cleo Level(2,Enemy,False) [8,9] 0x0 0x0 0x1 REDA9R10 [Argilabrys,IronAxe,Vulnerary]		NoAI
UNIT

BeginningScene:
COUNTER_SET 0 0
DismountAll
LOMA Ch5xBoatScenes
//beginning of boat scenes
RECLASS Cleo Skyseeker_Dismounted
RECLASS Loewe Scaleshot_Dismounted
LOAD1 0 LoeweBeginningCutsceneGroup
ENUN
FADU 15
BROWNBOXTEXT Ch18xDecianSea[8,8]
STAL 10
MUSC WaterFlowingSoundEffect
CAMERA Loewe
FlashCursor(Loewe,60)
Text(Ch5xLoewePrelude)
STAL 10
FlashCursor(9,8,60)
LOAD1 0 CleoBeginningCutsceneGroup
ENUN
STAL 10
MOVE 15 Cleo [7,5]
ENUN
MUSC FF6AwakeningID
Text(Ch5xLoeweCleoTalk)
MOVE 15 Cleo [9,8]
ENUN
HIDEUNIT Cleo
MOVE 15 Loewe [9,8]
ENUN
HIDEUNIT Loewe
STAL 10
MOVE 15 Kwame [8,5]
ENUN
CheckUnitDeadOrJailed(Mira)
BNE 20 s0 sC //branches if she is
	Text(Ch5xKwameSceneMiraAlive1)
	LOAD1 0 MiraBeginningCutsceneGroup
	ENUN
	MOVE 15 MiraCopy [8,6]
	ENUN
	Text(Ch5xKwameSceneMiraAlive2)
	MOVE 15 MiraCopy [9,8]
	ENUN
	REMOVEUNIT MiraCopy
	Text(Ch5xKwameSceneMiraAlive3)
	GOTO 21
LABEL 20
Text(Ch5xKwameSceneMiraDead)
LABEL 21
//MUSC WaterFlowingSoundEffect
CAMERA [18,8]
TILECHANGE 0
STAL 10
TILECHANGE 1
STAL 10
TILECHANGE 2
STAL 10
TILECHANGE 3
STAL 10
TILECHANGE 4
STAL 10
TILECHANGE 5
STAL 10
TILECHANGE 6
STAL 10
TILECHANGE 7
STAL 10
TILECHANGE 8
STAL 10
TILECHANGE 9
STAL 10
TILECHANGE 10
STAL 10
TILECHANGE 11
STAL 10
TILECHANGE 12
STAL 10
TILECHANGE 13
STAL 10
ENUN
CamOn
LOAD1 1 TrajanBeginningGroup
ENUN
BROWNBOXTEXT ElmhurstIslandText[8,8]
Text(Ch5xKwameGoesDown1)
MOVE 15 Kwame [9,8]
ENUN
REMOVEUNIT Kwame
Text(Ch5xKwameGoesDown2)
SPAWN_NPC Kwame [9,8]
MOVE 15 Kwame [9,11]
STAL 20
SHOWUNIT Loewe 
MOVE 15 Loewe [8,10]
STAL 20
SHOWUNIT Cleo
MOVE 15 Cleo [8,11]
ENUN
Text(Ch5xTrajanMeeting1)
TILECHANGE 14
MOVE 15 Loewe [16,10]
MOVE 15 Cleo [16,11]
MOVE 15 Kwame [15,10]
ENUN
CAMERA Trajan
FlashCursor(Trajan,60)
Text(Ch5xTrajanMeeting2)
CHECK_ALIVE Mira
BEQ 22 s0 sC
	CheckUnitJailed(Mira)
	BEQ 23 s0 sC
		Text(Ch5xTrajanMeeting3MiraCaptured)
		GOTO 24
	LABEL 23
	LOAD1 1 MiraBeginningCutsceneGroup
	ENUN
	MOVE 15 MiraCopy [15,11]
	ENUN
	Text(Ch5xTrajanMeeting3MiraAlive)
	GOTO 24
LABEL 22
Text(Ch5xTrajanMeeting3MiraDead)
LABEL 24
//MUSC WaterFlowingSoundEffect
Text(Ch5xTrajanMeeting4)
CamOff
MOVE 15 Senny [5,5]
MOVE 15 Helman [9,8]
ENUN
STAL 10
MOVE 15 Loewe [19,8]
MOVE 15 Cleo [19,9]
MOVE 15 Kwame [19,10]
MOVE 15 Trajan [19,11]
CheckUnitDeadOrJailed(Mira)
BNE 435 s0 sC // branches = not alive/present
	MOVE 15 MiraCopy [19,12]
LABEL 435
FadeOutMusic
FADI 15
ENUN
CheckUnitDeadOrJailed(Mira)
BNE 420 s0 sC // branches = not alive/present
	REMOVEUNIT MiraCopy
LABEL 420
RemoveAll
LOMA Ch5xInterlude //switches map to base map
LOAD1 0 HeadquartersBeginningCutsceneUnits
ENUN
FADU 5
BROWNBOXTEXT LiberatorBaseText[8,8]
Text(Ch5xTrajanMeeting5)
FlashCursor(8,1,60)
SingleUnitMove(Trajan, 15, 8, 1)
REMOVEUNIT Trajan
SingleUnitMove(Loewe, 15, 8, 1)
SVAL s1 0x1
SET_CONDITION Loewe
SingleUnitMove(Cleo, 15, 8, 1)
SVAL s1 0x1
SET_CONDITION Cleo
SingleUnitMove(Kwame, 15, 8, 1)
REMOVEUNIT Kwame
// beginning of war room scene
FADI 15
RemoveAll
TILECHANGE 0
LOAD1 2 WarRoomUnits
ENUN
FADU 5
MUSC FE5_InTheBattle_Fate
BROWNBOXTEXT SomeTimeLaterText[8,8]
FlashCursor(Trajan,60)
Text(Ch5xStrategyRoom)
MOVE 15 Cleo [8,9]
MOVE 15 Loewe [6,9]
STAL 60
HIDEUNIT Cleo
HIDEUNIT Loewe
MOVE 15 Kwame [8,9]
MOVE 15 Trajan [6,9]
STAL 60
REMOVEUNIT Kwame
REMOVEUNIT Trajan
ENUN
FADI 15
SHOWUNIT Cleo
SHOWUNIT Loewe
RemoveAll
TILEREVERT 0
ENUN
LOAD1 1 GreenUnits
LOAD1 1 CleoSelectionUnitGroup
ENUN
CheckUnitDeadOrJailed(Iskra)
BNE 1 s0 sC
	LOAD1 1 IskraCopyGroup
	ENUN
	UnitCopyASMC(Iskra,IskraCopy)
LABEL 1
CheckUnitDeadOrJailed(Mira)
BNE 2 s0 sC
	LOAD1 1 MiraCopyGroup
	ENUN
	UnitCopyASMC(Mira,MiraCopy)
LABEL 2
CheckUnitDeadOrJailed(Varga)
BNE 3 s0 sC
	LOAD1 1 VargaCopyGroup
	ENUN
	UnitCopyASMC(Varga,VargaCopy)
LABEL 3
HIDEUNIT Loewe
FADU 15
CamOff
NoFade
ENDA

EndingScene:
ENUT 0x84 //removes boss icons/talk icons etc
FadeOutMusic
FADI 15
RemoveAll
SHOWUNIT Loewe
LOAD1 0 LoeweEndingCutsceneGroup
ENUN
CAMERA Trajan
FADU 5
MUSC Rovers
Text(Ch5xLoeweCleoTrajanTalk1)
MOVE 15 Loewe [14,6]
MOVE 15 Cleo [14,7]
ENUN
SVAL s1 0x1
SET_CONDITION Loewe
SET_CONDITION Cleo
FlashCursor(Trajan,60)
Text(Ch5xLoeweCleoTrajanTalk2)
FADI 15
FadeOutMusic
RemoveAll
LOMA Ch5xGechrysos
LOAD1 1 GechrysosCutsceneGroup
ENUN
SetBackground(BlackBackground)
CenterTutorialTextBox
SOLOTEXTBOXSTART
TEXTSHOW Ch5xGechrysosText
TEXTEND
REMA
CALL $9EE2C4
MUSC WindyDaySoundEffect
BROWNBOXTEXT Ch5xImperialPalaceText[8,8]
CamOn
LOAD1 1 EliasUnitGroup
ENUN
FlashCursor(Helman,60)
Text(Ch5xEliasIntroduction)
MOVE 15 Helman [7,12]
ENUN
STAL 20
LOAD1 1 LeonidasUnitGroup
ENUN
MUSC FF4RedWingsID
Text(Ch5xLeonidasIntroduction)
SOUN 0x00B1
TILECHANGE 0
LOAD1 1 OrfeasUnitGroup
ENUN
Text(Ch5xOrfeasCoraliaIntroduction)
MOVE 15 Orfeas [8,9]
ENUN
REMOVEUNIT Orfeas
SOUN 0x00B1
TILEREVERT 0
STAL 10
Text(Ch5xLeonidasLeaves)
MOVE 15 Senny [14,11]
ENUN
REMOVEUNIT Senny
MUSC 0x7FFF
STAL 20
MUSC WindyDaySoundEffect
Text(Ch5xEliasCoraliaTalk)
MOVE 15 Helman [0,11]
ENUN
REMOVEUNIT Helman
STAL 30
Text(Ch5xCoraliaEndingText)
FADI 5
ENUF 0x84 //brings back boss icons/talk icons etc
MNC2 Chapter6
NoFade
ENDA
