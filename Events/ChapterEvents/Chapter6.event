#define COUNTER_CASE(counter, value) "COUNTER_CHECK counter; SVAL 0x1 value; BNE ((counter << 16) + value + 0x1234) 0xC 0x1;"
#define BREAK(counter, value) "LABEL ((counter << 16) + value + 0x1234);"

EventPointerTable(Chapter6Events, PointerList)

PointerList:
	POIN TurnBasedEvents
	POIN CharacterBasedEvents
	POIN LocationBasedEvents
	POIN MiscBasedEvents
	POIN Dunno1 Dunno2 Dunno3 Tutorial
	POIN Traps1 Traps2
	POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
	POIN $0 $0 $0
	POIN $0 $0 $0
	POIN BeginningScene EndingScene

TurnBasedEvents:
	TurnEventPlayer(0x0, TurnOne, 1)
	TurnEventPlayer(0x0, WipeCheck, 1, 255)
	TurnEventPlayer(10, StagingAreaEnter, 1, 255)
	TurnEventPlayer(11, StagingAreaExit, 1, 255)
	TurnEventPlayer(0x0, Ch6Reinforce2, 5)
	TurnEventPlayer(0x0, Ch6Reinforce3, 7)
	TurnEventPlayer(0x0, Ch6Reinforce4, 10)
	TurnEventPlayer(0x0, SpawnEnemies, 1, 255)
END_MAIN

TurnOne:
FadeOutMusic
TILEREVERT 1
CameraCursor(Barod)
Text(Ch6BarodIntro)
CAMERA_CENTERED [7,18]
CenterTextbox(Ch6ArenaHint)
//CALL SpawnEnemies
NoFade
ENDA

ALIGN 4
SpawnEnemies:
	CountEnemiesInRange(3,1,11,11)
	IBgt(0, 420)
	COUNTER_CASE(0, 0)
		CameraCursor(Barod)
		Text(Ch6BarodIntro1)
		SpawnReinforcements(Reinforce0)
	BREAK(0,0)
	COUNTER_CASE(0, 1)
		CameraCursor(Barod)
		Text(Ch6BarodCongratulations1)
		HealAllInArena
		STAL 30
		GiveMoney(1000)
		Text(Ch6BarodIntro2)
		SpawnReinforcements(Reinforce2)
	BREAK(0,1)
	COUNTER_CASE(0, 2)
		CameraCursor(Barod)
		HealAllInArena
		STAL 30
		Text(Ch6BarodCongratulations2)
		GiveMoney(1000)
		Text(Ch6BarodIntro3)
		SpawnReinforcements(Reinforce3)
	BREAK(0,2)
	COUNTER_CASE(0, 3)
		CameraCursor(Barod)
		HealAllInArena
		STAL 30
		Text(Ch6BarodCongratulations4)
		CHECK_ALIVE Leina
		BEQ 865 s0 sC
			CHECK_ALIVE Cromar
			BEQ 865 s0 sC //if cromar and leina are both alive, then do this
				CameraCursor(Leina)
				Text(Ch6BarodCongratulations5)
				GiveItem(SisterRing,Leina)
			GOTO 866
		LABEL 865
		GiveItem(SisterRing,Cleo)
		LABEL 866
		ENUT 7 //sister ring got flag
		CameraCursor(Barod)
		Text(Ch6BarodIntro4)
		SpawnReinforcements(Reinforce4)
	BREAK(0,3)
	COUNTER_CASE(0, 4)
		CameraCursor(Barod)
		HealAllInArena
		STAL 30
		Text(Ch6BarodFinalCongratulations)
	BREAK(0,4)
	Increment(0)
LABEL 420
NoFade
ENDA

ALIGN 4
StagingAreaEnter:
	CHECK_ALLEGIANCE -1
	IBne(0,0)
		CountPlayersInRange(6,13,8,15)
		IBne(3, 0)
			CountPlayersInRange(6,14,8,14)
			IBne(3, 0)
				TILECHANGE 1
				TILEREVERT 0
				GOTO 1
	LABEL 0
		UnsetFlag(0xA)
	LABEL 1
NoFade
ENDA

ALIGN 4
StagingAreaExit:
	CHECK_ALLEGIANCE -1
	IBne(0,0)
		CountPlayersInRange(1,0,13,12)
		IBne(0, 0)
			TILECHANGE 0
			TILEREVERT 1
			GOTO 1
	LABEL 0
		UnsetFlag(0xB)
	LABEL 1
NoFade
ENDA


ALIGN 4
WipeCheck:
	FlagIsSet(0xA, 0)
		CountPlayersInRange(1,0,13,14)
		IBgt(0, 0)
			TILECHANGE 0
			TILEREVERT 1
			UnsetFlag(0xA)
			GOTO 1
	LABEL 0
		CountPlayersInRange(6,14,8,14)
		BGT 1 sC s0 //if more than 1 player unit there, do not unset flag automatically
			UnsetFlag(0xB)
		LABEL 1
		CountPlayersInRange(6,14,8,14)
		SVAL s1 3
		BNE 2 s1 sC //if there are 3 exactly, unset the flag
			UnsetFlag(0xB)
		LABEL 2
NoFade
ENDA

Ch6Reinforce2:
SpawnReinforcements(PirateReinforce2)
SpawnReinforcements(PirateReinforce3)
NoFade
ENDA

PirateReinforce2:
UNIT Ch6PirateNoSkill Pirate Draken Level(6,Enemy,True) [11,32] 0x0 0x0 1 REDA11R31 [IronAxe, Torch] BrigandAI
UNIT

PirateReinforce3:
UNIT Ch6PirateNoSkill Pirate Draken Level(6,Enemy,True) [19,32] 0x0 0x0 1 REDA19R31 [IronAxe, Torch] BrigandAI
UNIT


Ch6Reinforce3:
SpawnReinforcements(PirateReinforce4)
SpawnReinforcements(PirateReinforce5)
NoFade
ENDA

PirateReinforce4:
UNIT Ch6Pirate Hunter Draken Level(6,Enemy,True) [18,32] 0x0 0x0 1 REDA18R32 [IronBow] ChargeAI
UNIT Ch6Pirate Myrmidon Draken Level(6,Enemy,True) [20,32] 0x0 0x0 1 REDA20R32 [IronSword] ChargeAI
UNIT

PirateReinforce5:
UNIT Ch6Pirate DarkMage Draken Level(6,Enemy,True) [12,32] 0x0 0x0 1 REDA12R32 [Flux] ChargeAI
UNIT Ch6Pirate Myrmidon Draken Level(6,Enemy,True) [10,32] 0x0 0x0 1 REDA10R32 [IronSword] ChargeAI
UNIT

Ch6Reinforce4:
SpawnReinforcements(PirateReinforce6)
SpawnReinforcements(PirateReinforce7)
NoFade
ENDA

PirateReinforce6:
UNIT Ch6Pirate Swordfighter Draken Level(6,Enemy,True) [12,32] 0x0 0x0 1 REDA12R32 [SteelSword] ChargeAI
UNIT Ch6Pirate Hunter Draken Level(6,Enemy,True) [10,32] 0x0 0x0 1 REDA10R32 [SteelBow] ChargeAI
UNIT

PirateReinforce7:
UNIT Ch6Pirate Archer Draken Level(6,Enemy,True) [18,32] 0x0 0x0 1 REDA18R32 [SteelBow] ChargeAI
UNIT Ch6Pirate Freelancer Draken Level(6,Enemy,True) [20,32] 0x0 0x0 1 REDA20R32 [SteelLance] ChargeAI
UNIT

ALIGN 32
CharacterBasedEvents:
CharacterEvent(9, LeinaRecruitCromar, Leina, Cromar)
END_MAIN

LeinaRecruitCromar:
	CHECK_ALIVE McCallum //is he dead, if so auto recruit
	IBne(0, 1)
			MUSC WithUs_Song
			FlashCursor(Cromar, 30)
			Text(LeinaRecruitCromarWithKill)
			CUSA Cromar
			SetFatigue(Cromar,0)
			GOTO 2
	LABEL 1
	CHECK_EVENTID 7 //is sister ring flag set
	IBeq(0, 0) //if so, recruit cromar and set global flag
			MUSC WithUs_Song
			FlashCursor(Cromar, 30)
			Text(LeinaRecruitCromarWithRing)
			CUSA Cromar
			SetFatigue(Cromar,0)
			FOGVISION 0
			RECLASS Cromar Hero
			ENUF 0x28
			FlashCursor(McCallum,30)
			Text(McCallumMovesOutOfTheWay)
			CUSN McCallum
			MOVE 20 McCallum [21, 0]
			ENUN
			ENUT 0x80
			REMOVEUNIT McCallum
			GOTO 2
	LABEL 0
	Text(CromarYellAtLeina)
	UnsetFlag(0x9)
	LABEL 2
NoFade
ENDA

LocationBasedEvents:
Armory(Ch6Armory1,10,16)
Vendor(Ch6Vendor1,11,16)
Village(16,Ch6Village1Event,10,24)
Village(17,Ch6Village2Event,21,25)
Village(18,Ch6Village3Event,27,20)
House(13,Ch6VeidTavernEvent,27,29)
LOCA 0xE IncrementSeizeCheck [27,3] 0x11
LOCA 0xF IncrementSeizeCheck [0,30] 0x11
END_MAIN

Ch6Village1Event:
VillageEventItem(Ch6Village1,House2,Greatsword)

Ch6Village2Event:
VillageEventItemDurability(Ch6Village2,House1,GrowthScroll,AsgeirScroll) 

Ch6Village3Event:
VillageEventItem(Ch6Village3,House1,KillerBow)

Ch6VeidTavernEvent:
SetBackground(Inn)
CHECK_EVENTID 12
BNE 777 s0 sC //if 12 is set, skip this part
	Text(Ch6VeidTalk1) //the intro veid talk
	ENUT 12
	CHECK_EVENTID 20 //if they somehow have it the first time, let's check for it
	BEQ 778 s0 sC
		Text(Ch6VeidTalk4)
		GiveItemWithDurability(SkillScroll,UmbraliumMightID,0xFFFF)
	LABEL 778
	ENUF 13
	GOTO 303
LABEL 777
CHECK_EVENTID 20
BEQ 776 s0 sC //if flag not set, branch past
	Text(Ch6VeidTalk3)
	GiveItemWithDurability(SkillScroll,UmbraliumMightID,0xFFFF)
	GOTO 303
LABEL 776
Text(Ch6VeidTalk2)
ENUF 13
LABEL 303
EraseBackground
NoFade
ENDA


IncrementSeizeCheck:
COUNTER_INC 2 //increments the counter when you seize
COUNTER_CHECK 2
SVAL s1 2
BNE 776 s1 sC //checks if we've seized both points (2 = 2), otherwise, branch past
	ENUT 3
	CALL EndingScene
LABEL 776
NoFade
ENDA

MiscBasedEvents:
CauseGameOverIfLordDies
AREA 10 StagingAreaEnter [6,14] [8,14]
AREA 11 StagingAreaExit [6,14] [8,14]
AREA 0 BarodSeethes [3,0] [11,5]
AFEV 1 DrakenQuoteGone 0x18 //dummy event triggered by flag 8, sets flag 1
AFEV 1 DrakenQuoteGone 0x19
AFEV 21 ZeusDefeated 20
END_MAIN

DrakenQuoteGone:
NoFade
ENDA

ZeusDefeated:
CameraCursor(Barod)
HealAllInArena
STAL 30
Text(Ch6BarodFinalCongratulations)
NoFade
ENDA

BarodSeethes:
CHECK_ALLEGIANCE (-1)
BNE 776 s0 sC // not 0 = not player unit
FlashCursor(Barod,60)
MUSS UnfinishedBattleGodSyndrome
Text(Ch6BarodSeethesText)
EARTHQUAKE 0 True
STAL 30
EARTHQUAKE 1 True
STAL 30
EARTHQUAKE 0 True
STAL 30
EARTHQUAKE 1 True
STAL 30
EARTHQUAKE_END
FlashCursor(Loewe,30)
Text(Ch6LoeweCleoReactToBarod)
SET_ACTIVE Loewe
KILL Cleo
AWAIT_KILL Cleo
ENUN
EARTHQUAKE 0 True
STAL 60
EARTHQUAKE_END
CALL GameOverStart
LABEL 776
NoFade
ENDA

Dunno1:
	END_MAIN

Dunno2:
	END_MAIN

Dunno3:
	END_MAIN

Tutorial:
	END_MAIN


ALIGN 4
Units1:
UNIT Cleo 0x0 Cleo Level(1,Ally,False) [6,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Loewe 0x0 Cleo Level(1,Ally,False) [7,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Leina 0 Cleo Level(1,Ally,False) [4,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [8,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [9,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [5,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [6,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [7,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [8,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [9,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [9,17] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [10,17] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [10,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT

PlayableLeina:
UNIT Leina 0 Cleo Level(4,Ally,False) [6,18] 0x0 0x0 0x0 0x1 [Rapier, Heal, Vulnerary] NoAI
UNIT

ArenaUnits:
UNIT Raschwitz_Ch1 Troubadour Cleo Level(5,NPC,False) [6,18] 0x0 0x0 0x0 0x1 [Rapier, Heal, Vulnerary] NoAI
UNIT Cromar 0x0 Cleo Level(1,NPC,False) [7,9] 0x0 0x0 0x0 0x1 [Dullahan, Elixir] BossHealAI
UNIT Barod 0x0 Barod Level(20,NPC,False) [7,6] 0x0 0x0 0x0 0x1 [Agni, Indra] BossNoAttackAI
UNIT Zeus Baron 0x0 Level(20,NPC,False) [7,3] 0x0 0x0 0 0 [Brenthunder] NoAI
UNIT Veid 0x0 0x0 Level(15,NPC,False) [18,18] 0x0 0x0 0x0 0x1 [CivilianProof] NoAI
UNIT

McCallumUnit:
UNIT McCallum 0x0 0x0 Level(1,Enemy,False) [21,0] 0x0 0x0 1 REDA27R3 [HandAxe, FullHelm, Elixir] [0x6,0x3,0x0,0x20]
UNIT

PlayerSceneUnits:
UNIT Cleo 0x0 Cleo Level(1,Ally,False) [3,16] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT

TestUnits:
UNIT McCallum 0x0 0x0 Level(1,Enemy,False) [7,20] 0x0 0x0 0 0 [HandAxe, FullHelm] BossAI
UNIT Draken 0x0 Draken Level(7,Enemy,False) [8,20] 0x0 0x0 0 0 [Hurlbat, SteelAxe] BossAI
UNIT

ArenaNPCs:
UNIT ArenaSpectator Civilian_F_2 Barod Level(1,NPC,False) [16,2] 0x0 0x0 0x0 0x1 [0x0] BossNoAttackAI
UNIT ArenaSpectator 0x0 Barod Level(1,NPC,False) [16,3] 0x0 0x0 0x0 0x1 [0x0] BossNoAttackAI
UNIT ArenaSpectator 0x0 Barod Level(1,NPC,False) [15,5] 0x0 0x0 0x0 0x1 [0x0] BossNoAttackAI
UNIT ArenaSpectator 0x0 Barod Level(1,NPC,False) [16,6] 0x0 0x0 0x0 0x1 [0x0] BossNoAttackAI
UNIT ArenaSpectator 0x0 Barod Level(1,NPC,False) [15,9] 0x0 0x0 0x0 0x1 [0x0] BossNoAttackAI
UNIT ArenaSpectator 0x0 Barod Level(1,NPC,False) [16,8] 0x0 0x0 0x0 0x1 [0x0] BossNoAttackAI
UNIT

Reinforce0:
UNIT Gladiator Fighter Barod Level(6,Enemy,True) [6,0] 0x0 0x0 0x0 0x0 [IronAxe] ChargeAI
UNIT Gladiator Fighter Barod Level(6,Enemy,True) [7,0] 0x0 0x0 0x0 0x0 [HandAxe, SteelAxe] ChargeAI
UNIT Gladiator Fighter Barod Level(6,Enemy,True) [8,0] 0x0 0x0 0x0 0x0 [IronAxe] ChargeAI
UNIT

Reinforce2:
UNIT Gladiator Myrmidon_F Barod Level(6,Enemy,True) [6,0] 0x0 0x0 0x0 0x0 [KillingEdge] ChargeAI
UNIT Gladiator Myrmidon Barod Level(6,Enemy,True) [7,0] 0x0 0x0 0x0 0x0 [IronSword] ChargeAI
UNIT Gladiator Swordfighter Barod Level(6,Enemy,True) [8,0] 0x0 0x0 0x0 0x0 [Longsword] ChargeAI
UNIT

Reinforce3:
UNIT Gladiator Shaman_F Barod Level(6,Enemy,True) [6,0] 0x0 0x0 0x0 0x0 [Flux] ChargeAI
UNIT Gladiator Sister Barod Level(6,Enemy,True) [8,0] 0x0 0x0 0x0 0x0 [SpellMirror] ChargeAI
UNIT Gladiator Mage_F Barod Level(5,Enemy,True) [7,0] 0x0 0x0 0x0 0x0 [AlairsGale] ChargeAI
UNIT

Reinforce4:
UNIT Zeus Baron Zeus Level(10,Enemy,False) [7,0] 0x0 0x0 1 REDA7R1 [Brenthunder] ChargeAI
UNIT


PirateEnemies1:
UNIT Draken 0x0 Draken Level(7,Enemy,False) [0, 32] 0x0 0x0 1 REDA0R30 [Hurlbat, SteelAxe, SkillScroll] BossAI
UNIT Ch6PirateNoSkill Swordfighter Draken Level(6,Enemy,True) [0,32] 0x0 0x0 1 REDA4R32 [SteelSword] AttackInRangeAI
UNIT Ch6PirateNoSkill Pirate Draken Level(6,Enemy,True) [0,32] 0x0 0x0 1 REDA5R31 [IronAxe] AttackInRangeAI
UNIT Ch6PirateNoSkill Hunter Draken Level(6,Enemy,True) [0,32] 0x0 0x0 1 REDA3R31 [IronBow] AttackInRangeAI

UNIT Ch6PirateNoSkill Brigand Draken Level(6,Enemy,True) [0,27] 0x0 0x0 1 REDA1R28 [HandAxe] AttackInRangeAI
UNIT Ch6PirateNoSkill DarkMage Draken Level(6,Enemy,True) [0,27] 0x0 0x0 0 0 [Flux] AttackInRangeAI

UNIT Ch6PirateNoSkill Freelancer Draken Level(6,Enemy,True) [0,27] 0x0 0x0 0x1 REDA4R24 [ShortLance] ChargeAI
UNIT Ch6PirateNoSkill Pirate Draken Level(6,Enemy,True) [0,27] 0x0 0x0 0x1 REDA5R25 [IronAxe] ChargeAI

UNIT Ch6PirateNoSkill DarkMage Draken Level(6,Enemy,True) [0,32] 0x0 0x0 1 REDA12R26 [Flux] ChargeAI
UNIT Ch6PirateNoSkill Swordfighter Draken Level(6,Enemy,True) [0,32] 0x0 0x0 1 REDA11R27 [SteelSword] ChargeAI
UNIT

PirateEnemies2:
UNIT Ch6Pirate Archer Draken Level(6,Enemy,True) [12,32] 0x0 0x0 0x0 0x1 [IronBow] AttackInRangeAI
UNIT Ch6Pirate Shaman Draken Level(6,Enemy,True) [10,32] 0x0 0x0 0x0 0x1 [Flux] AttackInRangeAI
UNIT

PirateEnemies3:
UNIT Ch6Pirate DarkMage Draken Level(6,Enemy,True) [20,32] 0x0 0x0 0x0 0x1 [Flux] AttackInRangeAI
UNIT Ch6Pirate Freelancer Draken Level(6,Enemy,True) [18,32] 0x0 0x0 0x0 0x1 [IronLance] AttackInRangeAI
UNIT


PirateEnemies4: //right side island
//left group
UNIT Ch6PirateNoSkill Freelancer Draken Level(6,Enemy,True) [27,29] 0x0 0x0 1 REDA19R24 [IronLance] ChargeAI
UNIT Ch6PirateNoSkill Hunter Draken Level(6,Enemy,True) [27,29] 0x0 0x0 1 REDA20R23 [SteelBow] ChargeAI

//tavern group
UNIT Ch6PirateNoSkill AxeFighter Draken Level(6,Enemy,True) [27,29] 0x0 0x0 1 REDA25R30 [SteelAxe] AttackInRangeAI
UNIT Ch6PirateNoSkill Freelancer Draken Level(6,Enemy,True) [27,29] 0x0 0x0 1 REDA26R31 [IronLance] AttackInRangeAI

//top group
UNIT Ch6PirateNoSkill DarkMage Draken Level(6,Enemy,True) [27,29] 0x0 0x0 1 REDA27R22 [Luna] AttackInRangeAI
UNIT Ch6PirateNoSkill Swordfighter Draken Level(6,Enemy,True) [27,29] 0x0 0x0 1 REDA28R23 [IronSword] AttackInRangeAI
UNIT Ch6PirateNoSkill Hunter Draken Level(6,Enemy,True) [27,29] 0x0 0x0 1 REDA28R21 [IronBow] AttackInRangeAI
UNIT

ALIGN 4
Traps1:
	GreenSigil(6,14)
	GreenSigil(7,14)
	GreenSigil(8,14)
	RedSigil(4,6)
	RedSigil(5,6)
	RedSigil(9,6)
	RedSigil(10,6)
	TelliusTorchOn(4,3,5)
	TelliusTorchOn(4,9,5)
	TelliusTorchOn(10,3,5)
	TelliusTorchOn(10,9,5)
	TelliusTorchOn(19,32,5)
	TelliusTorchOn(26,21,5)
	TelliusTorchOn(11,32,5)
	TelliusTorchOn(0,26,5)
	TelliusTorchOn(28,30,5)
ENDTRAP


ALIGN 4
Traps2:
ENDTRAP


ALIGN 4
Ch6Armory1:
SHORT IronSword
SHORT SteelSword
SHORT IronLance
SHORT SteelLance
SHORT IronAxe
SHORT SteelAxe
SHORT IronBow
SHORT SteelBow
SHORT Javelin
SHORT HandAxe
SHORT 0
SHORT 0

ALIGN 4
Ch6Vendor1:
SHORT Fire
SHORT Thunder
SHORT Lightning
SHORT Flux
SHORT Heal
SHORT Mend
SHORT Vulnerary
SHORT Elixir
SHORT PureWater
SHORT Torch
SHORT 0
SHORT 0

ALIGN 4
BeginningScene:
InterludeReturnRoutineASMC
//SHOWUNIT Varga
LOAD1 0 LoeweKwameUnits
LOAD1 1 ArenaNPCs
ENUN
//
//LOAD1 0x1 VeidUnit
CAMERA_CENTERED Loewe
ENUN
TILECHANGE 0
TILECHANGE 1
COUNTER_SET 0x0 0x0
COUNTER_SET 0x1 0x0
COUNTER_SET 0x2 0x0
SetFlag(0xB)
FADU 10
BROWNBOXTEXT YuriaIslandsText[8,8]
CameraCursor(Loewe)
MUSC SheenaID
Text(Ch6BeginningScene1)
MOVE 15 Loewe [15,7]
ENUN
Text(Ch6BeginningScene2)
MOVE 15 Kwame [27,10]
ENUN
REMOVEUNIT Kwame
CamOn
LOAD1 1 McCallumUnit
ENUN
CamOff
FlashCursor(McCallum,60)
Text(Ch6BeginningScene3)
LOAD1 0x1 ArenaUnits
ENUN
FADI 10
STAL 60
CAMERA_CENTERED Barod
FADU 10
CameraCursor(Barod)
Text(Ch6BeginningScene4)
FadeOutMusic
CamOn
MOVE 15 Cromar [7, 18]
ENUN
CameraCursor(Cromar)
MUSC FFTAnxiety
Text(Ch6BeginningScene5)
CamOff
MOVE 15 Cromar [17, 18]
MOVE 15 Raschwitz_Ch1 [3, 17]
ENUN
STAL 20
CameraCursor(Veid)
Text(Ch6BeginningScene7)
MOVE 15 Veid [27, 29]
ENUN
REMOVEUNIT Veid
STAL 1
Text(Ch6BeginningScene8)
CamOn
MOVE 15 Cromar [27,4]
ENUN
CameraCursor(McCallum)
Text(Ch6BeginningScene9)
CamOff
CameraCursor(Loewe)
Text(Ch6BeginningScene10)
FadeOutMusic
MOVE 15 Loewe [21,7]
ENUN
CameraCursor(Cromar)
MUSC Sortie_Formation
Text(Ch6BeginningScene11)
LOAD1 0 CleoCutsceneUnits
CamOn
MOVE 15 Loewe [8,19]
ENUN
CamOff
FlashCursor(Cleo,30)
FlashCursor(Iberis,30)
Text(Ch6BeginningScene12)
CamOn
LOAD1 1 PirateEnemies4
ENUN
LOAD1 1 PirateEnemies3
ENUN
LOAD1 1 PirateEnemies2
ENUN
LOAD1 1 PirateEnemies1
ENUN
CamOff
CameraCursor(Draken)
SetUnitItemDurability(Draken,2,AzuriumMightID)
Text(Ch6BeginningScene13)
CameraCursor(Cleo)
Text(Ch6BeginningScene14)
MOVE 15 Iberis [0,18]
ENUN
REMOVEUNIT Iberis
MOVE 15 Raschwitz_Ch1 [6,19]
ENUN
CameraCursor(Raschwitz_Ch1)
Text(Ch6BeginningScene15)
FADI 15
REMOVEUNIT Raschwitz_Ch1
REMOVEUNIT Zeus
LOAD1 1 PlayableLeina
ENUN
MountAll
GotoPrepScreen
NoFade
ENDA

LoeweKwameUnits:
UNIT Loewe Scaleshot 0 Level(6,Ally,False) [25,3] 0x0 0x0 0 0 [IronSword] NoAI
UNIT Kwame Civilian_2 0 Level(6,NPC,False) [25,4] 0x0 0x0 0 0 [IronLance] NoAI
UNIT

CleoCutsceneUnits:
UNIT Cleo Skyseeker 0 Level(6,Ally,False) [7,18] 0x0 0x0 0 0 [IronSword] NoAI
UNIT Iberis 0 0 Level(6,NPC,False) [6,19] 0x0 0x0 0 0 [IronLance] NoAI
UNIT 


EndingScene:
FADI 10
FadeOutMusic
CALL GiveBackKaiserschwertFake
CAMERA_CENTERED [27,3]
STAL 30
MUSC Explorer_RuinaForgottenRuinsID
CHECK_ALIVE Leina //need to check here for whether this pair is alive, as it would be awkward to have a scene where they don't acknowledge each other
BEQ 776 s0 sC //if leina is dead, skip this part
	CHECK_ALIVE Cromar //is cromar around
	BEQ 777 s0 sC //if he's not, then skip the good ending
		RemoveAll
		LoadGroup(LoeweCleoLeinaCromarGroup)
		FADU 10
		FlashCursor(Loewe,30)
		FlashCursor(Cleo,30)
		Text(Ch6EndingSceneLeinaCromarAlive1)
		MOVE 15 Leina [20,19]
		SingleUnitMove(Cromar, 15, 20, 20)
		Text(Ch6EndingSceneLeinaCromarAlive2)
		GOTO 420
	LABEL 777
		RemoveAll
		LoadGroup(LoeweCleoLeinaGroup)
		FADU 10
		FlashCursor(Loewe,30)
		FlashCursor(Cleo,30)
		Text(Ch6EndingSceneLeinaAlive1)
		NPCToAlly(Leina)
		SingleUnitMove(Leina, 15, 20, 19)
		Text(Ch6EndingSceneLeinaAlive2)
		GOTO 420
LABEL 776
CHECK_ALIVE Cromar
BEQ 778 s0 sC //if cromar isn't alive, and leina isn't alive, skip this
	RemoveAll
	LoadGroup(LoeweCleoCromarGroup)
	FADU 10
	FlashCursor(Loewe,30)
	FlashCursor(Cleo,30)
	Text(Ch6EndingSceneCromarAlive1)
	NPCToAlly(Cromar)
	SingleUnitMove(Cromar, 15, 20, 19)
	Text(Ch6EndingSceneCromarAlive2)
	GOTO 420
LABEL 778
	RemoveAll
	LoadGroup(LoeweCleoGroup)
	FADU 10
	FlashCursor(Loewe,30)
	FlashCursor(Cleo,30)
	Text(Ch6EndingSceneAllRecruitsDead)
LABEL 420
FadeOutMusic
FlashCursor(Loewe,60)
MUSC FF6AwakeningID
Text(Ch6LoeweAndCleoChatPostBattle)
LoadGroup(KwamePostBattleGroup)
CameraCursor(Kwame)
Text(Ch6EndingSceneKwamePopsOut)
FadeOutMusic
SingleUnitMove(Loewe, 15, 27, 10)
HIDEUNIT Loewe
LoadGroup(BarodIberisGroup)
FlashCursor(Barod,30)
FlashCursor(Iberis,30)
MUSC FF6EdgarAndSabin
Text(Ch6EndingSceneBarod1)
SingleUnitMove(Iberis, 15, 21, 0)
Text(Ch6EndingSceneBarod2)
MOVE 15 Barod [20,17]
MOVE 15 Iberis [24,3]
ENUN
Text(Ch6FinalEndingScene1)
FadeOutMusic
SingleUnitMoveThenErase(Iberis, 15, 27, 10)
MUSC KwameID
Text(Ch6FinalEndingScene2)
FADI 15
SHOWUNIT Loewe
RemoveAll
WM_LOADLOCATION2 0 Chapter8 0 //Required to fix chapter name
WM_SETDESTINATION 0 Chapter8 0 //World map node. Keep these equal.
WM_SETNEXTLOCATION 0 0 Chapter7 //Needed for world map narration.
MNCH Chapter7
NoFade
ENDA

LoeweCleoLeinaCromarGroup:
UNIT Cromar 0 Cleo Level(1,Ally,False) [27,4] 0x0 0x0 0x0 0x1 [Dullahan, Elixir] BossHealAI
UNIT Leina 0 Cleo Level(4,Ally,False) [27,5] 0x0 0x0 0x0 0x1 [Rapier, Heal, Vulnerary] NoAI
UNIT Loewe Scaleshot 0 Level(6,Ally,False) [26,3] 0x0 0x0 0 0 [IronSword] NoAI
UNIT Cleo Skyseeker 0 Level(6,Ally,False) [28,3] 0x0 0x0 0 0 [IronSword] NoAI
UNIT 

LoeweCleoLeinaGroup:
UNIT Leina 0 Cleo Level(4,NPC,False) [27,5] 0x0 0x0 0x0 0x1 [Rapier, Heal, Vulnerary] NoAI
UNIT Loewe Scaleshot 0 Level(6,Ally,False) [26,3] 0x0 0x0 0 0 [IronSword] NoAI
UNIT Cleo Skyseeker 0 Level(6,Ally,False) [28,3] 0x0 0x0 0 0 [IronSword] NoAI
UNIT 

LoeweCleoCromarGroup:
UNIT Cromar 0x0 Cleo Level(1,NPC,False) [27,4] 0x0 0x0 0x0 0x1 [Dullahan, Elixir] BossHealAI
UNIT Loewe Scaleshot 0 Level(6,Ally,False) [26,3] 0x0 0x0 0 0 [IronSword] NoAI
UNIT Cleo Skyseeker 0 Level(6,Ally,False) [28,3] 0x0 0x0 0 0 [IronSword] NoAI
UNIT 

LoeweCleoGroup:
UNIT Loewe Scaleshot 0 Level(6,Ally,False) [26,3] 0x0 0x0 0 0 [IronSword] NoAI
UNIT Cleo Skyseeker 0 Level(6,Ally,False) [28,3] 0x0 0x0 0 0 [IronSword] NoAI
UNIT

KwamePostBattleGroup:
UNIT Kwame Civilian_2 0 Level(6,NPC,False) [27,10] 0x0 0x0 1 REDA27R3 [IronLance] NoAI
UNIT

BarodIberisGroup:
UNIT Barod 0 0 Level(20,NPC,False) [21,17] 0x0 0x0 1 REDA24R4 [Agni, Indra] NoAI
UNIT Iberis 0 0 Level(6,NPC,False) [20,17] 0x0 0x0 1 REDA24R3 [0x0] NoAI
UNIT