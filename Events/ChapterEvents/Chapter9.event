EventPointerTable(Chapter9Events, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(13, MeldanSpawn,1,255)
TurnEventPlayer(0x0, BrigandSpawn, 2)
TurnEventPlayer(0x0, JennySpawn, 2)
TurnEventPlayer(0x0, Turn35Spawn, 3)
TurnEventPlayer(0x0, Turn46Spawn, 4)
TurnEventPlayer(0x0, Turn35Spawn, 5)
TurnEventPlayer(0x0, Turn46Spawn, 6)
TurnEventPlayer(0x0, Turn35Spawn, 7)
TurnEventPlayer(0x0, EliasWarning, 7)
TurnEventPlayer(0x0, EliasSpawn, 8)
END_MAIN

EliasWarning:
Text(Ch9EliasWarning)
NoFade
ENDA

JennySpawn:
SpawnReinforcements(JennyReinforce)
CHECK_EVENTID 0x78
BEQ 8 s0 sC
	CAMERA_CENTERED Jenny
	FlashCursor(Jenny,60)
	Text(JennySpawnText)
	SetAI(Jenny,6,0,4,0)
LABEL 8
NoFade
ENDA

MeldanSpawn:
CHECK_EVENTID 14
BEQ 776 s0 sC //if the event is not set, then branch
	SpawnReinforcements(MeldanReinforce)
	CHECK_EVENTID 0x79
	BEQ 9 s0 sC
		CAMERA_CENTERED Meldan
		FlashCursor(Meldan,60)
		Text(MeldanSpawnText)
		SetAI(Meldan,6,0,4,0)
		NoFade
		ENDB
	LABEL 9
LABEL 776
ENUF 13
NoFade
ENDA

EliasSpawn:
SpawnReinforcements(EliasGroup)
CameraCursor(Elias)
Text(Ch9EliasArrives)
CameraCursor(Cleo)
Text(Ch9ResponseToEliasArrival)
NoFade
ENDA

EliasGroup:
UNIT Elias Dracoknight 0x0 Level(10,Enemy,False) [0,6] 0x0 0x0 0x1 REDA2R6 [MasterAxe] PursueWithoutHeedAI
UNIT Cythes Dracoknight 0x0 Level(1,Enemy,True) [0,4] 0x0 0x0 0x1 REDA0R4 [Tomahawk] PursueWithoutHeedAI
UNIT Cythes Dracoknight 0x0 Level(1,Enemy,True) [0,5] 0x0 0x0 0x1 REDA1R5 [BraveAxe] PursueWithoutHeedAI
UNIT Cythes Dracoknight 0x0 Level(1,Enemy,True) [0,6] 0x0 0x0 0x1 REDA0R6 [Tomahawk] PursueWithoutHeedAI
UNIT Cythes Dracoknight 0x0 Level(1,Enemy,True) [0,7] 0x0 0x0 0x1 REDA1R7 [BraveAxe] PursueWithoutHeedAI
UNIT Cythes Dracoknight 0x0 Level(1,Enemy,True) [0,8] 0x0 0x0 0x1 REDA0R8 [Tomahawk] PursueWithoutHeedAI
UNIT

JennyReinforce:
UNIT Jenny RookKnight 0x0 Level(5,Enemy,False) [0,17] 0x0 0x0 0x1 REDA0R17 [ChargeLance,IronSword, Vulnerary] ChargeAI
UNIT

MeldanReinforce:
UNIT Meldan DukeKnight 0x0 Level(1,Enemy,False) [21,4] 0x0 0x0 0x1 REDA21R4 [BraveLance,PlatLance,Shortlance] ChargeAI
UNIT

BrigandSpawn:
CamOn
LOAD1 1 Brigand1Group
ENUN
STAL 10
LOAD1 1 Brigand2Group
ENUN
STAL 10
CAMERA_CENTERED [21,9]
FlashCursor(21,9,60)
Text(Ch9BrigandText)
CamOff
NoFade
ENDA

Brigand1Group:
UNIT Looter Brigand 0 Level(8,Enemy,True) [0,6] 0x0 0x0 0x0 0x0 [IronAxe] AttackThenLootAI
UNIT

Brigand2Group:
UNIT Looter Brigand 0 Level(8,Enemy,True) [21,9] 0x0 0x0 0x0 0x0 [IronAxe] AttackThenLootAI
UNIT

Turn46Spawn:	
SpawnReinforcements(Ch9Reinforce1)
NoFade
ENDA

Turn35Spawn:	
SpawnReinforcements(Ch9Reinforce2)
NoFade
ENDA


Ch9Reinforce1:
UNIT Cythes DragonRider Wrathgar Level(10,Enemy,True) [0,0] 0x0 0x0 0x1 REDA0R0 [SteelAxe] PursueWithoutHeedAI
UNIT Cythes DragonRider Wrathgar Level(10,Enemy,True) [16,0] 0x0 0x0 0x1 REDA16R0 [SteelAxe] PursueWithoutHeedAI
UNIT

Ch9Reinforce2:
UNIT Cythes RookKnight Wrathgar Level(10,Enemy,True) [11,0] 0x0 0x0 0x1 REDA11R0 [IronLance,SteelSword] PursueWithoutHeedAI
UNIT Cythes RookKnight Wrathgar Level(10,Enemy,True) [12,0] 0x0 0x0 0x1 REDA12R0 [SteelLance,IronSword] PursueWithoutHeedAI
UNIT


ALIGN 32
CharacterBasedEvents:
CharacterEventBothWays(7, JennyTalkCh9, Jenny, 0x0)
CharacterEventBothWays(8, MeldanTalkCh9, Meldan, 0x0)
END_MAIN

JennyTalkCh9:
MUSS WithUs_Song
Text(JennyRecruitText)
ENUT 10
ENUT 15
NPCToAlly(Jenny)
MURE 2
NoFade
ENDA

MeldanTalkCh9:
MUSS WithUs_Song
Text(MeldanRecruitText)
ENUT 11
ENUT 16
NPCToAlly(Meldan)
MURE 2
NoFade
ENDA

MiscBasedEvents:
AFEV 15 SetJennyFleeAI 10
AFEV 16 SetMeldanFleeAI 11
AREA 14 MeldanReinforcementSet [8, 0] [21, 13]
AFEV 1 SetGenericBossQ 20
AFEV 1 SetGenericBossQ 21
AFEV 1 SetGenericBossQ 22
AFEV 1 SetGenericBossQ 24
AFEV 1 SetGenericBossQ 25
CauseGameOverIfLordDies
END_MAIN


SetJennyFleeAI:
CHECK_EXISTS Jenny
BEQ 776 s0 sC
	Text(Ch9JennyFleesText)
	SetAI(Jenny,6,0xC,4,0)
LABEL 776
NoFade
ENDA

SetMeldanFleeAI:
CHECK_EXISTS Meldan
BEQ 776 s0 sC
	Text(Ch9MeldanFleesText)
	SetAI(Meldan,6,0xC,4,0)
LABEL 776
NoFade
ENDA

MeldanReinforcementSet:
CHECK_ALLEGIANCE (-1)
BEQ 776 s0 sC //if the unit is 0 (blue), branch and skip the unsetting of meldan reinforcement
	ENUF 14
LABEL 776
NoFade
ENDA

LocationBasedEvents:
Village(9,Ch9RedScarfVillage,2,1)
Village(12,Ch9KillerAxeVillage,20,15)
Escape(19,0)
Escape(20,0)
END_MAIN

Ch9RedScarfVillage:
VillageEventItem(Ch9RedScarfText,House1,RedScarf)

Ch9KillerAxeVillage:
VillageEventItem(Ch9KillerAxeText,House2,KillerAxe)

Dunno1:

END_MAIN

Dunno2:

END_MAIN

Dunno3:

END_MAIN

Tutorial:

END_MAIN

Units1:
UNIT Cleo 0x0 Cleo Level(1,Ally,False) [2,16] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Loewe 0x0 Cleo Level(1,Ally,False) [4,16] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [1,15] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [5,17] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [3,17] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [1,17] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [3,15] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [4,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [2,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [5,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [3,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [1,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT

EnemyUnits:
UNIT Wrathgar Warrior 0x0 Level(1,Enemy,False) [20,1] 0x0 0x0 0 0 [Greatbow,SteelAxe, SecretBook] AttackInRangeAI

UNIT Militia Hunter Wrathgar Level(7,Enemy,True) [0,4] 0x0 0x0 0x0 0x0 [IronBow] TwoActionAI 
UNIT Militia Hunter Wrathgar Level(7,Enemy,True) [10,2] 0x0 0x0 0x0 0x0 [ShortBow] TwoActionAI 
UNIT Militia Hunter Wrathgar Level(7,Enemy,True) [18,3] 0x0 0x0 0x0 0x0 [IronBow] TwoActionAI
UNIT Militia Hunter Wrathgar Level(7,Enemy,True) [21,10] 0x0 0x0 0x0 0x0 [Shortbow] TwoActionAI 
UNIT Militia Hunter Wrathgar Level(7,Enemy,True) [16,19] 0x0 0x0 0x0 0x0 [Shortbow] TwoActionAI 

UNIT Militia Archer Wrathgar Level(7,Enemy,True) [6,3] 0x0 0x0 0x0 0x0 [IronBow] TwoActionAI
UNIT Militia Archer Wrathgar Level(7,Enemy,True) [14,2] 0x0 0x0 0x0 0x0 [Shortbow] TwoActionAI
UNIT Militia Archer Wrathgar Level(7,Enemy,True) [21,6] 0x0 0x0 0x0 0x0 [Shortbow] TwoActionAI 
UNIT Militia Archer Wrathgar Level(7,Enemy,True) [21,16] 0x0 0x0 0x0 0x0 [IronBow] TwoActionAI 

UNIT Militia LanceSoldier Wrathgar Level(7,Enemy,True) [4,3] 0x0 0x0 0x0 0x0 [IronLance] AttackInRangeAI 
UNIT Militia LanceSoldier Wrathgar Level(7,Enemy,True) [7,11] 0x0 0x0 0x0 0x0 [ShortLance] GroupOneAI 
UNIT Militia LanceSoldier Wrathgar Level(7,Enemy,True) [11,17] 0x0 0x0 0x0 0x0 [IronLance] GroupOneAI 

UNIT Militia AxeSoldier Wrathgar Level(7,Enemy,True) [2,9] 0x0 0x0 0x0 0x0 [IronAxe] AttackInRangeAI 
UNIT Militia AxeSoldier Wrathgar Level(7,Enemy,True) [9,15] 0x0 0x0 0x0 0x0 [ShortAxe] GroupOneAI 
UNIT Militia AxeSoldier Wrathgar Level(7,Enemy,True) [16,16] 0x0 0x0 0x0 0x0 [IronAxe] AttackInRangeAI 



UNIT Cythes AxeArmor Wrathgar Level(10,Enemy,True) [7,6] 0x0 0x0 0x0 0x0 [HandAxe] BossAI
UNIT Cythes LanceArmor Wrathgar Level(10,Enemy,True) [12,11] 0x0 0x0 0x0 0x0 [Javelin] BossAI
UNIT Cythes AxeArmor Wrathgar Level(10,Enemy,True) [17,9] 0x0 0x0 0x0 0x0 [HandAxe] BossAI
UNIT Cythes LanceArmor Wrathgar Level(10,Enemy,True) [20,8] 0x0 0x0 0x0 0x0 [Javelin] BossAI

UNIT Cythes RookKnight Wrathgar Level(10,Enemy,True) [19,1] 0x0 0x0 0x0 0x0 [SteelSword, SteelLance] AttackInRangeAI
UNIT Cythes RookKnight Wrathgar Level(10,Enemy,True) [17,6] 0x0 0x0 0x0 0x0 [IronSword, SteelLance] AttackInRangeAI
UNIT Cythes RookKnight Wrathgar Level(10,Enemy,True) [16,11] 0x0 0x0 0x0 0x0 [IronLance, SteelSword] AttackInRangeAI

UNIT Cythes DragonRider Wrathgar Level(10,Enemy,True) [0,0] 0x0 0x0 0x0 0x0 [SteelAxe] AttackInRangeAI
UNIT Cythes DragonRider Wrathgar Level(10,Enemy,True) [16,0] 0x0 0x0 0x0 0x0 [SteelAxe] AttackInRangeAI
UNIT

Traps1:
BlueSigil(19,0)
BlueSigil(20,0)
RedSigil(3,0)
RedSigil(12,0)
RedSigil(0,6)
RedSigil(21,9)
ENDTRAP
ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4

BeginningScene:
COUNTER_SET 0 0 //for escape purposes
MountAll
LOAD1 1 InitialMapUnits
ENUN
SetBackground(CampTent)
MUSC FF6EdgarAndSabin
Text(Ch9BeginningSceneLoeweCleoKwameDiscussion)
FADI 15
FadeOutMusic
CAMERA_CENTERED [0,17]
STAL 60
EraseBackground
CamOn
MOVE 15 Korovai [17,15]
MOVE 15 Epony [18,16]
ENUN
CamOff
CAMERA_CENTERED Korovai
FlashCursor(Korovai,30)
FlashCursor(Epony,30)
MUSC WindyDayBirdsSoundEffect
Text(Ch9BeginningSceneKorovaiEponySpeakWithVillager)
LOAD1 1 WrathgarUnits
ENUN
Text(Ch9BeginningSceneWrathgarShowsUp)
FadeOutMusic
StartBattle 
NormalDamage(0,25)
EndAttack
SetScriptedBattle(90,25,0,0,0,0)
FIGHT_MAP Wrathgar Raschwitz SteelAxe 0
ENUN
AWAIT_KILL Raschwitz
MUSC Sortie_Formation
Text(Ch9BeginningSceneWrathgarKilledAMan)
MOVE 15 Helman [19,0]
ENUN
Text(Ch9BeginningSceneWrathgarGloats)
MOVE 15 Wrathgar [19,0]
MOVE 15 Militia [19,0]
MOVE 15 Senny [19,0]
ENUN
Text(Ch9BeginningSceneKorovaiEponyResponseToWrathgar)
MOVE 15 Korovai [2,16]
MOVE 15 Epony [2,17]
FADI 15
ENUN
STAL 60
SetBackground(VillageRoad)
Text(Ch9BeginningSceneLoeweCleoRespondToComingThreat)
FADI 15
RemoveAll
ENUN
LOAD1 0x1 EnemyUnits
ENUN
//Senny Rescued
CHECK_EVENTID 0x78 
BNE 4 s0 sC // s0 always 0, sC = 1 if flag is set, 0 if not
ENUT 7 // sets flag so the talk isn't possible if the flag isn't set
LABEL 4 
//Helman Rescued
CHECK_EVENTID 0x79 
BNE 5 s0 sC // s0 always 0, sC = 1 if flag is set, 0 if not
ENUT 8 // sets flag so the talk isn't possible if the flag isn't set
LABEL 5
GotoPrepScreen
ENDA

InitialMapUnits:
UNIT Korovai DukeKnight 0 Level(10,NPC,True) [0,16] 0x0 0x0 0x0 0x0 [SteelSword, SteelLance] AttackInRangeAI
UNIT Epony Noble 0 Level(10,NPC,True) [0,17] 0x0 0x0 0x0 0x0 [SteelSword, SteelLance] AttackInRangeAI
UNIT Raschwitz Civilian_2 0 Level(10,NPC,True) [19,14] 0x0 0x0 0x0 0x0 [0x0] AttackInRangeAI
UNIT 

WrathgarUnits:
UNIT Wrathgar Warrior 0x0 Level(1,Enemy,False) [19,0] 0x0 0x0 1 REDA19R13 [SteelAxe,Greatbow, SecretBook] AttackInRangeAI
UNIT Militia AxeSoldier Wrathgar Level(7,Enemy,True) [20,0] 0x0 0x0 1 REDA18R12 [IronAxe] AttackInRangeAI 
UNIT Helman AxeSoldier Wrathgar Level(7,Enemy,True) [19,0] 0x0 0x0 1 REDA19R12 [ShortAxe] GroupOneAI 
UNIT Senny AxeSoldier Wrathgar Level(7,Enemy,True) [20,0] 0x0 0x0 1 REDA20R12 [IronAxe] AttackInRangeAI 
UNIT

EndingScene:
FADI 15
ENUT 3
ENUN
EndOfEscapeRoutineASMC
ENUN
STAL 1
SHOWUNIT Loewe
SHOWUNIT Cleo //just in case... i've seen loewe disappear here once or twice
CALL GiveBackKaiserschwertFake
RemoveAllDouble
SetBackground(0x22)
MUSC FE5_InTheBattle_Fate
Text(Ch9EndingSceneLoeweCleoBriefDiscussion)
FADI 15
STAL 60
SetBackground(0x23)
Text(Ch9EndingSceneLoeweCleoArgueWithKorovaiEpony1)
FadeOutMusic
STAL 60
MUSC oppression
Text(Ch9EndingSceneKorovaiEponyConfrontation1)
FADI 15
FadeOutMusic
LOMA Chapter10
LOAD1 1 HornetValseGroup
ENUN
CAMERA_CENTERED Hornet
FADU 15
FlashCursor(Hornet,30)
FlashCursor(Valse,30)
MUSC FFTAnxiety
Text(Ch9EndingSceneMifanScene1)
LOAD1 1 MifanGroup
ENUN
//check some flags
CHECK_EVENTID 0xA5
BEQ 1 s0 sC //if it's not set, skip it
	LOAD1 1 WellworthGroup
LABEL 1
CHECK_EVENTID 0xA3
BEQ 2 s0 sC //if it's not set, skip it
	LOAD1 1 YorGroup
LABEL 2
CHECK_EVENTID 0xA4
BEQ 3 s0 sC //if it's not set, skip it
	LOAD1 1 TheGiantGroup
LABEL 3
CHECK_EVENTID 0xA2
BEQ 4 s0 sC //if it's not set, skip it
	LOAD1 1 BuldakGroup
LABEL 4
ENUN
CameraCursor(Mifan)
Text(Ch9EndingSceneMifanScene2)
FADI 15
WM_LOADLOCATION2 0 Chapter11 0 //Required to fix chapter name
WM_SETDESTINATION 0 Chapter11 0 //World map node. Keep these equal.
WM_SETNEXTLOCATION 0 0 Chapter10 //Needed for world map narration.
MNCH Chapter10
NoFade
ENDA

HornetValseGroup:
UNIT Valse Freelancer Cleo Level(8,Enemy,False) [11,5] 0x0 0x0 0x0 0x1 [Greatlance,Javelin,Vulnerary] NeverMoveAI
UNIT Hornet BowKnight Cleo Level(11,Enemy,False) [9,5] 0x0 0x0 0x0 0x1 [Stinger,GaleBow,Vulnerary] DoNotAttack07AI
UNIT Cythes SwordArmor Cleo Level(11,Enemy,False) [9,6] 0x0 0x0 0x0 0x1 [Stinger,GaleBow,Vulnerary] NeverMoveAI
UNIT Cythes Hunter Cleo Level(11,Enemy,False) [10,6] 0x0 0x0 0x0 0x1 [Stinger,GaleBow,Vulnerary] NeverMoveAI
UNIT Cythes LanceArmor Cleo Level(11,Enemy,False) [11,6] 0x0 0x0 0x0 0x1 [Stinger,GaleBow,Vulnerary] NeverMoveAI
UNIT Cythes Axefighter Cleo Level(11,Enemy,False) [9,7] 0x0 0x0 0x0 0x1 [Stinger,GaleBow,Vulnerary] NeverMoveAI
UNIT Cythes LanceSoldier Cleo Level(11,Enemy,False) [10,7] 0x0 0x0 0x0 0x1 [Stinger,GaleBow,Vulnerary] NeverMoveAI
UNIT Cythes Swordfighter Cleo Level(11,Enemy,False) [11,7] 0x0 0x0 0x0 0x1 [Stinger,GaleBow,Vulnerary] NeverMoveAI
UNIT Cythes LanceArmor Cleo Level(11,Enemy,False) [9,8] 0x0 0x0 0x0 0x1 [Stinger,GaleBow,Vulnerary] NeverMoveAI
UNIT Cythes Archer Cleo Level(11,Enemy,False) [10,8] 0x0 0x0 0x0 0x1 [Stinger,GaleBow,Vulnerary] NeverMoveAI
UNIT Cythes AxeArmor Cleo Level(11,Enemy,False) [11,8] 0x0 0x0 0x0 0x1 [Stinger,GaleBow,Vulnerary] NeverMoveAI
UNIT Cythes Hero_F Cleo Level(11,Enemy,False) [10,9] 0x0 0x0 0x0 0x1 [Stinger,GaleBow,Vulnerary] NeverMoveAI
UNIT


MifanGroup:
UNIT Mifan Swordmaster_F 0x0 Level(1,Enemy,False) [9,0] 0x0 0x0 1 REDA10R4 [IceSword,KillingEdge] BossAI
UNIT

WellworthGroup:
UNIT Wellworth Hero 0x0 Level(1,Enemy,False) [8,0] 0x0 0x0 1 REDA9R3 [StoneSword] NeverMoveAI
UNIT

YorGroup:
UNIT Yor Halberdier 0x0 Level(1,Enemy,False) [7,0] 0x0 0x0 1 REDA8R3 [YorsYari] NeverMoveAI
UNIT

TheGiantGroup:
UNIT TheGiant Warrior 0x0 Level(1,Enemy,False) [9,0] 0x0 0x0 1 REDA11R3 [SteelBow,TrainedAxe] NeverMoveAI
UNIT

BuldakGroup:
UNIT Buldak ThiefFighter 0x0 Level(1,Enemy,False) [10,0] 0x0 0x0 1 REDA12R3 [Buldirk] NeverMoveAI
UNIT