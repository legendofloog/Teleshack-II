EventPointerTable(Chapter9Events, PointerList)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(13, MeldanSpawn,1,255)
TurnEventPlayer(0x0, BrigandSpawn, 2)
TurnEventPlayer(0x0, JennySpawn, 2)
TurnEventPlayer(0x0, Turn357Spawn, 3)
TurnEventPlayer(0x0, Turn246Spawn, 4)
TurnEventPlayer(0x0, Turn357Spawn, 5)
TurnEventPlayer(0x0, Turn246Spawn, 6)
TurnEventPlayer(0x0, Turn357Spawn, 7)
TurnEventPlayer(0x0, Turn8910Spawn, 8)
TurnEventPlayer(0x0, Turn8910Spawn, 9)
TurnEventPlayer(0x0, Turn8910Spawn, 10)
TurnEventPlayer(0x0, Turn111213Spawn,11)
TurnEventPlayer(0x0, Turn111213Spawn,12)
TurnEventPlayer(0x0, Turn111213Spawn,13)
END_MAIN

JennySpawn:
SpawnReinforcements(JennyReinforce)
CHECK_EVENTID 0x78
BEQ 8 s0 sC
	CAMERA_CENTERED Jenny
	FlashCursor(Jenny,60)
	Text(JennySpawnText)
	SetAI(Jenny,6,0,4,0)
LABEL 8
NoFade
ENDA

MeldanSpawn:
CHECK_EVENTID 14
BEQ 776 s0 sC //if the event is not set, then branch
	SpawnReinforcements(MeldanReinforce)
	CHECK_EVENTID 0x79
	BEQ 9 s0 sC
		CAMERA_CENTERED Meldan
		FlashCursor(Meldan,60)
		Text(MeldanSpawnText)
		SetAI(Meldan,6,0,4,0)
		NoFade
		ENDB
	LABEL 9
LABEL 776
ENUF 13
NoFade
ENDA

JennyReinforce:
UNIT Jenny RookKnight 0x0 Level(5,Enemy,False) [0,17] 0x0 0x0 0x1 REDA0R17 [LongLance,Javelin,IronSword] ChargeAI
UNIT

MeldanReinforce:
UNIT Meldan DukeKnight 0x0 Level(1,Enemy,False) [21,4] 0x0 0x0 0x1 REDA21R4 [Greatlance,BraveLance,LightRune] ChargeAI
UNIT

BrigandSpawn:
CamOn
LOAD1 1 Brigand1Group
ENUN
STAL 10
LOAD1 1 Brigand2Group
ENUN
STAL 10
CAMERA_CENTERED [21,9]
FlashCursor(21,9,60)
Text(Ch9BrigandText)
CamOff
NoFade
ENDA

Brigand1Group:
UNIT Looter Brigand 0 Level(8,Enemy,True) [0,6] 0x0 0x0 0x0 0x0 [SteelAxe] AttackThenLootAI
UNIT

Brigand2Group:
UNIT Looter Brigand 0 Level(8,Enemy,True) [21,9] 0x0 0x0 0x0 0x0 [SteelAxe] AttackThenLootAI
UNIT

Turn246Spawn:	
SpawnReinforcements(Ch9Reinforce1)
NoFade
ENDA

Turn357Spawn:	
SpawnReinforcements(Ch9Reinforce2)
NoFade
ENDA

Turn8910Spawn:	
SpawnReinforcements(Ch9Reinforce3)
NoFade
ENDA

Turn111213Spawn:
SpawnReinforcements(Ch9Reinforce4)
NoFade
ENDA

Ch9Reinforce1:
UNIT Cythes PegasusRider Wrathgar Level(9,Enemy,True) [0,1] 0x0 0x0 0x1 REDA0R1 [IronLance] PursueWithoutHeedAI
UNIT Cythes PegasusRider Wrathgar Level(9,Enemy,True) [0,0] 0x0 0x0 0x1 REDA0R0 [SteelLance] PursueWithoutHeedAI
UNIT Cythes PegasusRider Wrathgar Level(9,Enemy,True) [15,0] 0x0 0x0 0x1 REDA15R0 [IronLance] PursueWithoutHeedAI
UNIT Cythes PegasusRider Wrathgar Level(9,Enemy,True) [16,0] 0x0 0x0 0x1 REDA16R0 [SteelLance] PursueWithoutHeedAI
UNIT

Ch9Reinforce2:
UNIT Cythes RookKnight Wrathgar Level(9,Enemy,True) [0,16] 0x0 0x0 0x1 REDA0R16 [IronLance,SteelSword] PursueWithoutHeedAI
UNIT Cythes RookKnight Wrathgar Level(9,Enemy,True) [0,17] 0x0 0x0 0x1 REDA0R17 [SteelLance,IronSword] PursueWithoutHeedAI
UNIT Cythes RookKnight Wrathgar Level(9,Enemy,True) [11,0] 0x0 0x0 0x1 REDA11R0 [IronLance,SteelSword] PursueWithoutHeedAI
UNIT Cythes RookKnight Wrathgar Level(9,Enemy,True) [12,0] 0x0 0x0 0x1 REDA12R0 [SteelLance,IronSword] PursueWithoutHeedAI
UNIT

Ch9Reinforce3:
UNIT Cythes GoldKnight Wrathgar Level(1, Enemy,True) [0,16] 0x0 0x0 0x1 REDA0R16 [KillingEdge,MasterLance] PursueWithoutHeedAI
UNIT Cythes GoldKnight Wrathgar Level(1, Enemy,True) [0,17] 0x0 0x0 0x1 REDA0R17 [KillerLance,MasterSword] PursueWithoutHeedAI
UNIT

Ch9Reinforce4:
UNIT Cythes Dracoknight Wrathgar Level(1, Enemy,True) [0,0] 0x0 0x0 0x1 REDA0R0 [KillerAxe,BraveAxe] PursueWithoutHeedAI
UNIT Cythes Dracoknight Wrathgar Level(1, Enemy,True) [0,1] 0x0 0x0 0x1 REDA0R1 [KillerAxe,BraveAxe] PursueWithoutHeedAI
UNIT


ALIGN 32
CharacterBasedEvents:
CharacterEventBothWays(7, JennyTalkCh9, Jenny, 0x0)
CharacterEventBothWays(8, MeldanTalkCh9, Meldan, 0x0)
END_MAIN

JennyTalkCh9:
MUSS WithUs_Song
Text(JennyRecruitText)
ENUT 10
ENUT 15
CUSA Jenny
SetFatigue(Jenny,0)
MURE 2
NoFade
ENDA

MeldanTalkCh9:
MUSS WithUs_Song
Text(MeldanRecruitText)
ENUT 11
ENUT 16
CUSA Meldan
SetFatigue(Meldan,0)
MURE 2
NoFade
ENDA

MiscBasedEvents:
AFEV 15 SetJennyFleeAI 10
AFEV 16 SetMeldanFleeAI 11
AREA 14 MeldanReinforcementSet [8, 0] [21, 13]
CauseGameOverIfLordDies
END_MAIN

SetJennyFleeAI:
CHECK_EXISTS Jenny
BEQ 776 s0 sC
	Text(Ch9JennyFleesText)
	SetAI(Jenny,6,0xC,4,0)
LABEL 776
NoFade
ENDA

SetMeldanFleeAI:
CHECK_EXISTS Jenny
BEQ 776 s0 sC
	Text(Ch9MeldanFleesText)
	SetAI(Meldan,6,0xC,4,0)
LABEL 776
NoFade
ENDA

MeldanReinforcementSet:
CHECK_ALLEGIANCE (-1)
BEQ 776 s0 sC //if the unit is 0 (blue), branch and skip the unsetting of meldan reinforcement
	ENUF 14
LABEL 776
NoFade
ENDA

LocationBasedEvents:
Village(9,Ch9RedScarfVillage,2,1)
Village(12,Ch9KillerAxeVillage,20,15)
Escape(19,0)
Escape(20,0)
END_MAIN

Ch9RedScarfVillage:
VillageEventItem(Ch9RedScarfText,House1,RedScarf)

Ch9KillerAxeVillage:
VillageEventItem(Ch9KillerAxeText,House2,KillerAxe)

Dunno1:

END_MAIN

Dunno2:

END_MAIN

Dunno3:

END_MAIN

Tutorial:

END_MAIN

Units1:
UNIT Cleo 0x0 Cleo Level(1,Ally,False) [2,16] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Loewe 0x0 Cleo Level(1,Ally,False) [4,16] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [1,15] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [5,17] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [3,17] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [1,17] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [3,15] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [4,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [2,18] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [5,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [3,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT Iskra 0x0 Cleo Level(1,Ally,False) [1,19] 0x0 0x0 0x0 0x1 [0x0] NoAI
UNIT

EnemyUnits:
//Boss
UNIT Wrathgar Warrior 0x0 Level(1,Enemy,False) [20,1] 0x0 0x0 0x0 0x0 [Greatbow,SteelAxe] AttackInRangeAI
UNIT Cythes AxeArmor Wrathgar Level(9,Enemy,True) [19,1] 0x0 0x0 0x0 0x0 [BattleAxe] AttackInRangeAI


UNIT Militia Hunter Wrathgar Level(6,Enemy,True) [1,4] 0x0 0x0 0x0 0x0 [IronBow]TwoActionAI 
UNIT Militia Hunter Wrathgar Level(6,Enemy,True) [10,2] 0x0 0x0 0x0 0x0 [IronBow,Vulnerary]TwoActionAI 
UNIT Militia Hunter Wrathgar Level(6,Enemy,True) [18,3] 0x0 0x0 0x0 0x0 [Shortbow]TwoActionAI 
UNIT Militia Hunter Wrathgar Level(6,Enemy,True) [17,19] 0x0 0x0 0x0 0x0 [Shortbow,Vulnerary]TwoActionAI 

UNIT Militia Archer Wrathgar Level(6,Enemy,True) [6,3] 0x0 0x0 0x0 0x0 [IronBow]TwoActionAI 
UNIT Militia Archer Wrathgar Level(6,Enemy,True) [20,18] 0x0 0x0 0x0 0x0 [IronBow,Vulnerary]TwoActionAI 
UNIT Militia Archer Wrathgar Level(6,Enemy,True) [13,2] 0x0 0x0 0x0 0x0 [Shortbow]TwoActionAI 
UNIT Militia Archer Wrathgar Level(6,Enemy,True) [16,8] 0x0 0x0 0x0 0x0 [Shortbow,Vulnerary]TwoActionAI 

UNIT Militia LanceSoldier Wrathgar Level(6,Enemy,True) [2,8] 0x0 0x0 0x0 0x0 [ShortLance]TwoActionAI 
UNIT Militia LanceSoldier Wrathgar Level(6,Enemy,True) [11,16] 0x0 0x0 0x0 0x0 [ShortLance,Vulnerary]TwoActionAI 
UNIT Militia LanceSoldier Wrathgar Level(6,Enemy,True) [7,10] 0x0 0x0 0x0 0x0 [IronLance]TwoActionAI 

UNIT Militia AxeSoldier Wrathgar Level(6,Enemy,True) [11,13] 0x0 0x0 0x0 0x0 [IronAxe]TwoActionAI 
UNIT Militia AxeSoldier Wrathgar Level(6,Enemy,True) [19,5] 0x0 0x0 0x0 0x0 [IronAxe,Vulnerary]TwoActionAI 
UNIT Militia AxeSoldier Wrathgar Level(6,Enemy,True) [14,18] 0x0 0x0 0x0 0x0 [IronAxe]TwoActionAI 
UNIT Militia AxeSoldier Wrathgar Level(6,Enemy,True) [12,7] 0x0 0x0 0x0 0x0 [IronAxe,Vulnerary]TwoActionAI 

UNIT Cythes SwordArmor Wrathgar Level(9,Enemy,True) [4,3] 0x0 0x0 0x0 0x0 [Longsword] AttackInRangeAI

UNIT Cythes LanceArmor Wrathgar Level(9,Enemy,True) [16,16] 0x0 0x0 0x0 0x0 [SteelLance] AttackInRangeAI
UNIT Cythes LanceArmor Wrathgar Level(9,Enemy,True) [20,8] 0x0 0x0 0x0 0x0 [SteelLance] AttackInRangeAI

UNIT Cythes SwordKnight Wrathgar Level(9,Enemy,True) [8,4] 0x0 0x0 0x0 0x0 [Armorslayer] AttackInRangeAI

UNIT Cythes AxeKnight Wrathgar Level(9,Enemy,True) [13,9] 0x0 0x0 0x0 0x0 [SteelAxe] AttackInRangeAI
UNIT Cythes AxeKnight Wrathgar Level(9,Enemy,True) [20,13] 0x0 0x0 0x0 0x0 [IronAxe] AttackInRangeAI

UNIT Cythes PegasusRider Wrathgar Level(9,Enemy,True) [0,0] 0x0 0x0 0x0 0x0 [IronLance] TwoActionAI
UNIT Cythes PegasusRider Wrathgar Level(9,Enemy,True) [16,0] 0x0 0x0 0x0 0x0 [IronLance] TwoActionAI
UNIT

Traps1:
BlueSigil(19,0)
BlueSigil(20,0)
RedSigil(3,0)
RedSigil(12,0)
RedSigil(0,6)
RedSigil(21,9)
ENDTRAP
ALIGN 4

Traps2:
	ENDTRAP
	ALIGN 4

BeginningScene:
	COUNTER_SET 0 0 //for escape purposes
	MountAll
	LOAD1 0x1 EnemyUnits
	ENUN
	SetBackground(VillageRoad)
	MUSC WindyDayBirdsSoundEffect
	Text(Ch9OpeningText)
	FADI 15
	SetBackground(CastleHallway)
	MUSC Sortie_Formation
	Text(Ch9WrathgarRant)
	FADI 15
	SetBackground(OvergrownArches)
	Text(MeldanWorryText)
	FADI 15
	ENUN
	//Senny Rescued
	CHECK_EVENTID 0x78 
	BNE 4 s0 sC // s0 always 0, sC = 1 if flag is set, 0 if not
	ENUT 7 // sets flag so the talk isn't possible if the flag isn't set
	LABEL 4 
	//Helman Rescued
	CHECK_EVENTID 0x79 
	BNE 5 s0 sC // s0 always 0, sC = 1 if flag is set, 0 if not
	ENUT 8 // sets flag so the talk isn't possible if the flag isn't set
	LABEL 5 
	GotoPrepScreen
	ENDA

EndingScene:
EndOfEscapeRoutineASMC
SetBackground(CastleHallway)
MUSC FF5_Nostalgia
Text(Ch9EndingText)
FADI 15
SetBackground(OvergrownArches)
Text(Ch9EndingText2)
MNC2 Chapter10
NoFade
ENDA
